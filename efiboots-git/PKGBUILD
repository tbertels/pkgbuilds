# https://aur.archlinux.org/packages/efiboots-git
groups=('modified')

pkgname=efiboots-git
pkgver=1.0.r23.g0c0335f
pkgrel=1
pkgdesc="Manage EFI boot loader entries with this simple GUI"
arch=('any')
url="https://github.com/Elinvention/efiboots"
license=('GPL3')
depends=('efibootmgr' 'gtk4' 'python-gobject')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/Elinvention/efiboots.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/${pkgname%-git}"
  python -m build --wheel --no-isolation
}

package() {
  cd "$srcdir/${pkgname%-git}"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Don't install tests
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rf "${pkgdir}${site_packages}/test"
}
