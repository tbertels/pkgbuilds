# https://aur.archlinux.org/packages/system76-power-git
groups=('modified')

pkgname=system76-power-git
pkgver=1.1.20.r3.g4bd008f
pkgrel=1
pkgdesc="System76 Power Management"
arch=('any')
url='https://github.com/pop-os/system76-power'
license=('GPL')
depends=('dbus' 'libusb' 'systemd')
makedepends=('cargo' 'git')
optdepends=('system76-acpi-dkms' 'system76-dkms')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=('!lto')
source=('git+https://github.com/pop-os/system76-power.git'
        'makefile.patch'
        'mkinitcpio.patch')
sha256sums=('SKIP'
            '59c2b6a4ee4d5bf728d8f68fd9c066ba33cf9c91bf173bf16734924f6e641643'
            'fefd4bd1c173cca2aafd3d5f03af20b9cfc5553f9a53f510a12d565cc90fe7ba')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/${pkgname%-git}"

  # use mkinitcpio -P inplace of update-initramfs -u
  patch -Np1 -i "${srcdir}/mkinitcpio.patch"

  # fix makefile
  patch -Np1 -i "${srcdir}/makefile.patch"

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$srcdir/${pkgname%-git}"
  export RUSTUP_TOOLCHAIN=stable
  make
}

package() {
  cd "$srcdir/${pkgname%-git}"
  make sysconfdir="/usr/lib" DESTDIR="${pkgdir}" install
}
