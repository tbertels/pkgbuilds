pkgname=skiff-desktop
_app_id=com.fyralabs.SkiffDesktop
pkgver=0.1.7
pkgrel=1
pkgdesc="Privacy-first end-to-end encrypted email"
arch=('x86_64')
url="https://skiff.com/mail"
license=('GPL3')
depends=('libhelium' 'webkitgtk-6.0')
makedepends=('blueprint-compiler' 'meson' 'vala')
checkdepends=('appstream')
source=("$pkgname-$pkgver.tar.gz::https://github.com/FyraLabs/skiff-desktop/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('592184fe33a618955293ea50281967395acd0f37a268f02d0116c69d07dfb199')

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build

  # A path, if given, must begin and end with a slash
  sed -i 's/SkiffDesktop/SkiffDesktop\//g' "build/data/${_app_id}.gschema.xml"
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"

  ln -s "/usr/bin/${_app_id}" "$pkgdir/usr/bin/$pkgname"
}
