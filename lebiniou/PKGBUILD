# https://aur.archlinux.org/packages/lebiniou
groups=('modified')

pkgname=lebiniou
pkgver=3.56.1
pkgrel=1
pkgdesc="A tool to create images from sound"
arch=('x86_64')
url="https://biniou.net"
license=('GPL')
depends=('libmagick6' 'fftw' 'sdl2_ttf' 'libcaca' 'ffmpeg'
         'lebiniou-data>=3.56.0' 'ulfius')
makedepends=('git' 'uglify-js' 'nodejs-clean-css-cli' 'htmlmin')
source=("git+https://gitlab.com/lebiniou/lebiniou.git#tag=version-$pkgver")
sha256sums=('SKIP')

build() {
	cd "$srcdir/$pkgname"
	./bootstrap
	export PKG_CONFIG_PATH="$PKG_CONFIG_PATH:/usr/lib/imagemagick6/pkgconfig"
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname"
	make DESTDIR="$pkgdir/" install
}

