# https://aur.archlinux.org/packages/libadwaita-git
groups=('modified')

pkgname=libadwaita-git
pkgver=1.0.0.alpha.1.r0.gfaa9f26a
pkgrel=1
pkgdesc="Library full of GTK+ widgets for mobile phones"
url="https://gitlab.gnome.org/GNOME/libadwaita"
license=('LGPL')
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
depends=('gtk4')
makedepends=('git' 'gobject-introspection' 'meson' 'sassc' 'vala' 'wayland-protocols')
#checkdepends=(xorg-server-xvfb)
provides=("${pkgname%-git}" "${pkgname%-git}-1.so")
conflicts=("${pkgname%-git}")
source=('git+https://gitlab.gnome.org/GNOME/libadwaita.git')
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/${pkgname%-git}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    arch-meson "${pkgname%-git}" build \
        -Dtests=false \
        -Dexamples=false
    meson compile -C build
}

#check() {
#    dbus-run-session xvfb-run \
#      -s '-screen 0 1920x1080x24 -nolisten local' \
#      meson test -C build --print-errorlogs
#}

package() {
    DESTDIR="$pkgdir" meson install -C build
}
