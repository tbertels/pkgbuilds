# https://aur.archlinux.org/pkgbase/mangohud
groups=('modified')

pkgname=('mangohud' 'lib32-mangohud' 'mangohud-common')
pkgbase=mangohud
pkgver=0.6.3
pkgrel=1
pkgdesc="A Vulkan overlay layer for monitoring FPS, temperatures, CPU/GPU load and more."
arch=('x86_64')
url="https://github.com/flightlessmango/MangoHud"
license=('MIT')
makedepends=('meson' 'python-mako' 'glslang' 'libglvnd' 'lib32-libglvnd'
             'libxnvctrl' 'vulkan-headers' 'dbus' 'lib32-dbus'
             'lib32-vulkan-icd-loader')
source=("$pkgbase-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('6dbb2b496a75340c135a4bf8d85e0f52a23108449171aef535a3940df2d21daa')

build() {
	arch-meson --wrap-mode default \
		-Duse_system_vulkan=enabled \
		"MangoHud-$pkgver" build64
	meson compile -C build64

	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
	export LLVM_CONFIG="/usr/bin/llvm-config32"

	arch-meson --wrap-mode default \
		--libdir lib32 \
		-Duse_system_vulkan=enabled \
		-Dinclude_doc=false \
		"MangoHud-$pkgver" build32
	meson compile -C build32
}

package_mangohud() {
	depends=('mangohud-common' 'dbus' 'gcc-libs' 'libx11' 'vulkan-icd-loader')
	optdepends=('libxnvctrl: Support for older NVIDIA GPUs')

	DESTDIR="$pkgdir" meson install -C build64

	rm -rf "$pkgdir"/usr/share/{doc,man}
}

package_lib32-mangohud() {
	depends=('mangohud-common' 'lib32-dbus' 'lib32-gcc-libs' 'lib32-libx11'
	         'lib32-vulkan-icd-loader')
	optdepends=('lib32-libxnvctrl: Support for older NVIDIA GPUs')

	DESTDIR="$pkgdir" meson install -C build32

	rm -rf "$pkgdir"/usr/{bin,share/man}
	mv "$pkgdir/usr/share/vulkan/implicit_layer.d/MangoHud.json" \
		"$pkgdir/usr/share/vulkan/implicit_layer.d/MangoHud.x86.json"
}

package_mangohud-common() {
	pkgdesc="Common files for MangoHud"

	DESTDIR="$pkgdir" meson install -C build64

	rm -rf "$pkgdir"/usr/{bin,lib,share/vulkan}

	cd "MangoHud-$pkgver"
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgbase"
}
