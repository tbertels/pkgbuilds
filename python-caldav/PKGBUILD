# https://aur.archlinux.org/packages/python-caldav
groups=('modified')

pkgname=python-caldav
_name=${pkgname#python-}
pkgver=0.9.0
pkgrel=1
pkgdesc="A CalDAV (RFC4791) client library for Python."
arch=('any')
url="https://github.com/python-caldav/caldav"
license=('Apache')
depends=('python' 'python-lxml' 'python-pytz' 'python-requests' 'python-six'
         'python-tzlocal' 'python-vobject')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
#optdepends=('python-icalendar') # not yet on the official dependency list
#checkdepends=('python-coverage' 'python-icalendar' 'python-nose' 'radicale' 'xandikos')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('6053493d1247fc28796cac58982677b21854dc05e14bb35190fcda4a3f4c6c51')

build() {
  cd "$_name-$pkgver"
  python -m build --wheel --no-isolation
}

# Tests take about 10 minutes, test_caldav.py test fails

#check() {
#  cd "$_name-$pkgver"
#  nosetests
#}

package() {
  cd "$_name-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
