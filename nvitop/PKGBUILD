# https://aur.archlinux.org/packages/nvitop
groups=('modified')

pkgname=nvitop
pkgver=1.3.2
pkgrel=1
pkgdesc="An interactive NVIDIA-GPU process viewer and beyond, the one-stop solution for GPU process management."
arch=('any')
url="https://github.com/XuehaiPan/nvitop"
license=('GPL3')
depends=('ncurses' 'python-cachetools' 'python-psutil' 'python-pynvml'
         'python-termcolor')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('cf3e1ddf54ce2b4acf8ab25e84a5d596a0b98bc4d5b711eb0d256b3cab22ce23')

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
