# https://aur.archlinux.org/packages/gnome-shell-extension-panel-corners-git
groups=('modified')

pkgname=gnome-shell-extension-panel-corners-git
_uuid=panel-corners@aunetx
pkgver=9.r0.g5880fdb
pkgrel=1
pkgdesc="A gnome-shell extension to keep the old topbar corners, which were removed for GNOME 42"
arch=('any')
url="https://github.com/aunetx/panel-corners"
license=('GPL3')
depends=('gnome-shell>=1:45.0')
makedepends=('git' 'zip')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/aunetx/panel-corners.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/panel-corners"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/panel-corners"
  make build
}

package() {
  cd "$srcdir/panel-corners"
  install -d "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}"
  bsdtar -xvf "pkg/${_uuid}.zip" -C "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}"

  install -Dm644 schemas/org.gnome.shell.extensions.panel-corners.gschema.xml -t \
    "$pkgdir/usr/share/glib-2.0/schemas/"

  rm -rf "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/schemas/"
}
