# https://aur.archlinux.org/packages/toggle-git
groups=('modified')

pkgname=toggle-git
_app_id=io.gitlab.orowith2os.Toggle
pkgver=r105.f035d2b
pkgrel=1
pkgdesc="Like Tweaks, but with more Libadwaita!"
arch=('any')
url="https://gitlab.com/OroWith2Os/toggle"
license=('GPL3')
depends=('gjs' 'libadwaita')
makedepends=('blueprint-compiler' 'git' 'meson' 'setconf' 'typescript')
checkdepends=('appstream-glib')
optdepends=('gnome-shell-extensions: enable Light Theme')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://gitlab.com/OroWith2Os/toggle.git'
        'git+https://gitlab.gnome.org/BrainBlasted/gi-typescript-definitions.git'
        'fix-build.patch')
b2sums=('SKIP'
        'SKIP'
        'e8f69a7f9e789251ef7e5060e1c681bbc1806371021f3bc1abf66fadb4dd48535c9e270cf9bf64beb9b6d67d469bc1c92526f9cec81baa2ac50e0862d53c2215')

pkgver() {
  cd "${pkgname%-git}"
  ( set -o pipefail
    git describe --long --abbrev=7 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd "${pkgname%-git}"
  git submodule init
  git config submodule.src/gi-types.url "$srcdir/gi-typescript-definitions"
  git -c protocol.file.allow=always submodule update

  patch --forward --strip=1 --input="$srcdir/fix-build.patch"

  # Add icon name to desktop file as '@app_id@' is not defined
  setconf data/io.gitlab.orowith2os.Toggle.desktop.in.in Icon "${_app_id}"
}

build() {
  arch-meson "${pkgname%-git}" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Binary name is currently toggle, not io.gitlab.orowith2os.Toggle 
  # like the desktop file thinks.
  # When upstream resolves the issue, the symlink should 
  # be removed or reversed.
  ln -s "/usr/bin/${pkgname%-git}" "$pkgdir/usr/bin/${_app_id}"
}
