pkgname=manjaro-i3-gnome
_pkgname=${pkgname#manjaro-}
pkgver=40
pkgrel=1
pkgdesc="Use i3 with GNOME Session integration: Manjaro edition."
arch=('any')
url="https://github.com/i3-gnome/i3-gnome"
license=('MIT')
depends=('gnome-session'
         'gnome-settings-daemon'

         # manjaro-i3-settings packages:
         'adapta-maia-theme'
         'conky-i3'
         'dmenu-manjaro'
         'dunst'
         'gsfonts'
         'i3-default-artwork'
         'i3exit'
         'i3-scripts'
         'i3-scrot'
         'i3-gaps'
         'i3-help'
         'i3status-manjaro'
         'nerd-fonts-terminus'
         #'nitrogen'
         'papirus-maia-icon-theme'
         'urxvt-perls'
         'xcursor-breeze'
         'xorg-fonts-misc')
makedepends=('git')
optdepends=('gdm'
            'gnome-flashback'

            # manjaro-i3-settings packages:
            'artwork-i3: Manjaro-i3 wallpapers'
            'manjaro-backgrounds: Collection of Manjaro wallpapers'
            'vertex-maia-icon-theme: Icons matching Vertex Maia themes, based on Menda Circle'

            # extras
            'bmenu'
            'morc_menu'
            'network-manager-applet'
            'volumeicon'
            'sgtk-menu')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$_pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('0cbde5f704a878f6af03e48507c3a1345850f16def2649d3336fe8a1cf9cc8a5')

build() {
	cd "$_pkgname-$pkgver"
	make
}

package() {
	cd "$_pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"

	echo 'Explore https://gitlab.manjaro.org/profiles-and-settings/desktop-settings/tree/master/community/i3/skel for config files'
}

