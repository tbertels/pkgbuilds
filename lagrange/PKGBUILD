# https://aur.archlinux.org/packages/lagrange
groups=('modified')

pkgname=lagrange
pkgver=1.15.1
pkgrel=1
pkgdesc="Beautiful Gemini Client"
arch=('x86_64' 'aarch64')
url="https://gmi.skyjake.fi/lagrange"
license=('BSD')
depends=('fribidi' 'harfbuzz' 'hicolor-icon-theme' 'libwebp' 'mpg123' 'openssl'
         'pcre2' 'sdl2' 'zlib')
makedepends=('cmake' 'git' 'zip')
install="$pkgname.install"
source=("https://git.skyjake.fi/skyjake/lagrange/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz"{,.sig})
sha256sums=('4c697d579da1babcd21bfb0c76fd98be0283c3170d52d64a29ff3a13d733a910'
            'SKIP')
validpgpkeys=('15674AE498667047A3EB9431BACCFCFB98DB2EDC') # Jaakko Ker√§nen (skyjake) <jaakko.keranen@iki.fi>

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname-$pkgver"
  install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
