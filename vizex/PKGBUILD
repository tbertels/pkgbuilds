# https://aur.archlinux.org/packages/vizex
groups=('modified')

pkgname=vizex
pkgver=2.0.5
pkgrel=1
pkgdesc="Visualize disk space and disk usage in your UNIX\Linux terminal"
arch=('any')
url="https://github.com/bexxmodd/vizex"
license=('MIT')
depends=('python-click' 'python-colored' 'python-pandas' 'python-psutil'
         'python-magic-ahupp' 'python-tabulate')
makedepends=('python-setuptools')
#checkdepends=('python-pytest')
_commit=d3e7de09767ffbc7cbb04efaec009d30f882b3bb
source=("https://pypi.org/packages/source/${pkgname:0:1}/$pkgname/$pkgname-$pkgver.tar.gz"
        "https://raw.githubusercontent.com/bexxmodd/vizex/$_commit/requirements.txt")
sha256sums=('1264820d82aaee52be258795ebfab3439be6aeec046af27ffb3bd8a11cf2cd47'
            '9efdfc25893d53699561a7ab87f8a48566a45f93d7ffe681749295a838280c2a')

prepare() {
  cd "$pkgname-$pkgver"

  # Proper module name
  find . -type f -exec sed -i "s|main|$pkgname|g" {} \;
  mv main "$pkgname"

  cp "$srcdir/requirements.txt" .
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

#check() {
#  cd "$pkgname-$pkgver"
#  pytest
#}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
