# https://aur.archlinux.org/packages/vizex
groups=('modified')

pkgname=vizex
pkgver=2.0.4
pkgrel=1
pkgdesc="Visualize disk space and disk usage in your UNIX\Linux terminal"
arch=('any')
url="https://github.com/bexxmodd/vizex"
license=('MIT')
depends=('python-click' 'python-colored' 'python-pandas' 'python-psutil'
         'python-magic-ahupp' 'python-tabulate')
makedepends=('python-setuptools')
#checkdepends=('python-pytest')
_commit=e3373b84c1536cb07021e9ff2134e950150ac32b
source=("https://pypi.org/packages/source/${pkgname:0:1}/$pkgname/$pkgname-$pkgver.tar.gz"
        "https://raw.githubusercontent.com/bexxmodd/vizex/$_commit/requirements.txt")
sha256sums=('ba48ba8579cfeea2d4a86fde45ac68e53e384bb14fa45496cb33a4e8dacddddd'
            '9efdfc25893d53699561a7ab87f8a48566a45f93d7ffe681749295a838280c2a')

prepare() {
  cd "$pkgname-$pkgver"

  # Proper module name
  find . -type f -exec sed -i "s|main|$pkgname|g" {} \;
  mv main "$pkgname"

  cp "$srcdir/requirements.txt" .
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

#check() {
#  cd "$pkgname-$pkgver"
#  pytest
#}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
