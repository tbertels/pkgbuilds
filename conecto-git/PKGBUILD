pkgname=conecto-git
pkgver=r112.5cd1e48
pkgrel=1
pkgdesc="Integrate your Android phone with your PC: a GTK client for KDEConnect."
arch=('x86_64')
url="https://github.com/hannesschulze/conecto"
license=('GPL2')
depends=('folks' 'granite' 'gtkmm3' 'jsoncpp')
makedepends=('git' 'meson')
checkdepends=('appstream-glib')
provides=("${pkgname%-git}" 'libconecto.so')
conflicts=("${pkgname%-git}")
source=('git+https://github.com/hannesschulze/conecto.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  arch-meson "${pkgname%-git}" build \
    --wrap-mode=nofallback \
    -Ddisable_plank_support=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs ||:
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  ln -s /usr/bin/com.github.hannesschulze.conecto "$pkgdir/usr/bin/${pkgname%-git}"
}
