# https://aur.archlinux.org/pkgbase/protonutils
groups=('modified')

pkgname=protonutils
pkgver=1.6.2
pkgrel=1
pkgdesc="Lists configured Proton version per game."
arch=('x86_64')
url="https://github.com/nning/protonutils"
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
_commit=18647bb1f6237c85cb146612d3dda79a9b71b2bc
source=("git+https://github.com/nning/protonutils.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd "$srcdir/$pkgname"
  export GOPATH="$srcdir/gopath"
  go mod tidy
}

build() {
  cd "$srcdir/$pkgname"
  make build_pie

  # Clean module cache for makepkg -C
  go clean -modcache
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/usr" install

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"

  # fix permissions
  find "$pkgdir/" -type d -exec chmod 755 {} \;
  find "$pkgdir/usr/share/" -type f -exec chmod 644 {} \;
}
