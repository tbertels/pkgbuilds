# https://aur.archlinux.org/pkgbase/protonutils
groups=('modified')

pkgname=protonutils
pkgver=1.2.2
pkgrel=2
pkgdesc="Lists configured Proton version per game."
arch=('x86_64')
url="https://github.com/nning/protonutils"
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        'https://github.com/nning/protonutils/pull/3.patch')
sha256sums=('516702108778006646107e1143a726df2281386cd4b07d815883b827349ab143'
            'f9b9e6009413b1612c4c964ae7e9f1ea1ea63fc6983f2874b38f349df5156f0c')

prepare() {
  cd "$pkgname-$pkgver"

  # Prevent creation of a `go` directory in one's home.
  # Sometimes this directory cannot be removed with even `rm -rf` unless
  # one becomes root or changes the write permissions.
  export GOPATH="$srcdir/gopath"
  go clean -modcache

  # Ignore key not found in localconfig.vdf
  patch -Np1 -i ../3.patch
}

build() {
  cd "$pkgname-$pkgver"
  local VERSION="v$pkgver"
  local BUILDTIME=$(date -u +"%Y%m%d%H%M%S")
  local GOLDFLAGS="-X 'main.Version=${VERSION}' \
                   -X 'main.Buildtime=${BUILDTIME}'"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" ${GOLDFLAGS}" \
    -o build ./cmd/...

  # Generate man page
  ./build/"$pkgname" -m man1

  # Clean mod cache for makepkg -C
  go clean -modcache
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "build/$pkgname" -t "$pkgdir/usr/bin/"
  install -Dm644 man1/* -t "$pkgdir/usr/share/man/man1/"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
