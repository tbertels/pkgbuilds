# https://aur.archlinux.org/pkgbase/protonutils
groups=('modified')

pkgname=protonutils
pkgver=1.6.7
pkgrel=1
pkgdesc="Lists configured Proton version per game."
arch=('x86_64')
url="https://github.com/nning/protonutils"
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
_commit=7ea1ccaf8f219748d13a6a51c9da471141f8eb64  # tags/1.6.7^0
source=("git+https://github.com/nning/protonutils.git#commit=${_commit}?signed")
sha256sums=('SKIP')
validpgpkeys=('CCBB87A93F9F494C93EEA47905CCCF87AE5FC712') # henning mueller <henning@orgizm.net>

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd "$srcdir/$pkgname"
  export GOPATH="$srcdir/gopath"
  go mod tidy
}

build() {
  cd "$srcdir/$pkgname"
  export GOPATH="$srcdir/gopath"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  make build_pie DESTDIR=usr

  # Makefile only generates ZSH completions
  "./cmd/$pkgname/$pkgname" completion bash > completion.bash
  "./cmd/$pkgname/$pkgname" completion fish > completion.fish

  # Clean module cache for makepkg -C
  go clean -modcache
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/usr" install

  # Fix permissions
  chmod 0644 "$pkgdir"/usr/share/man/man1/*.1
  chmod 0755 "$pkgdir/usr/share/man/man1"

  install -Dm644 completion.bash "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm644 completion.fish "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
