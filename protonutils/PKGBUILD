# https://aur.archlinux.org/pkgbase/protonutils
groups=('modified')

pkgname=protonutils
pkgver=1.6.2
pkgrel=1
pkgdesc="Lists configured Proton version per game."
arch=('x86_64')
url="https://github.com/nning/protonutils"
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
_commit=18647bb1f6237c85cb146612d3dda79a9b71b2bc
source=("git+https://github.com/nning/protonutils.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd "$srcdir/$pkgname"
  export GOPATH="$srcdir/gopath"
  go mod tidy
}

build() {
  cd "$srcdir/$pkgname"
  make build_pie
  make man
  make completion

  "./cmd/$pkgname/$pkgname" completion bash > completion.bash
  "./cmd/$pkgname/$pkgname" completion fish > completion.fish

  # Clean module cache for makepkg -C
  go clean -modcache
}

package() {
  cd "$srcdir/$pkgname"
  install -Dm755 "cmd/$pkgname/$pkgname" -t "$pkgdir/usr/bin/"
  install -Dm644 man1/* -t "$pkgdir/usr/share/man/man1/"
  install -Dm644 completion.bash "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm644 completion.fish "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -Dm644 completion.zsh "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
