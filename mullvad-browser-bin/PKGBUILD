# https://aur.archlinux.org/packages/mullvad-browser-bin
groups=('modified')

pkgname=mullvad-browser-bin
pkgver=12.0.4
pkgrel=3
pkgdesc='Privacy-focused web browser developed in a collaboration between Mullvad VPN and the Tor Project.'
arch=('x86_64')
url="https://mullvad.net/browser"
license=('GPL' 'LGPL' 'MPL')
depends=('dbus-glib' 'ffmpeg' 'gtk3' 'libpulse' 'libxt' 'mime-types' 'nss' 'ttf-font')
optdepends=(
  'hunspell-en_US: Spell checking, American English'
  'libnotify: Notification integration'
  'networkmanager: Location detection via available WiFi networks'
  'pulseaudio: Audio support'
  'speech-dispatcher: Text-to-Speech'
  'xdg-desktop-portal: Screensharing with Wayland')
provides=("${pkgname%-bin}")
conflicts=("${pkgname%-bin}")
source=("https://cdn.mullvad.net/browser/$pkgver/${pkgname%-bin}-linux64-${pkgver}_ALL.tar.xz"{,.asc}
        "${pkgname%-bin}.sh"
        "${pkgname%-bin}.desktop")
sha256sums=('ab875328d42472a691c22176e6254e412bf054b037b494654380f30b7b6e1b90'
            'SKIP'
            'a75809f5de63ea43d3591124a7076668df1a0acf959060756254dc7a9d94a82b'
            '7d6036df369a79bd54625f621479eff5c02a93ad749da3315f0e5ce14ba1f241')
validpgpkeys=('EF6E286DDA85EA2A4BA7DE684E2C6E8793298290')  # Tor Browser Developers (signing key) <torbrowser@torproject.org>

package() {
  cd "$srcdir/${pkgname%-bin}"
  install -d "$pkgdir/opt/${pkgname%-bin}"
  cp -a Browser/* "$pkgdir/opt/${pkgname%-bin}/"

  # by default, only owner has access to all files
  chmod -R ugo+r "$pkgdir/opt/${pkgname%-bin}"
  find "$pkgdir/opt/${pkgname%-bin}" -executable -execdir chmod ugo+x '{}' +

  # ask it to create profiles in ~
  touch "$pkgdir/opt/${pkgname%-bin}/system-install"

  # cli wrapper
  install -Dm755 "$srcdir/${pkgname%-bin}.sh" "$pkgdir/usr/bin/${pkgname%-bin}"

  # desktop file for various launchers
  install -Dm644 "$srcdir/${pkgname%-bin}.desktop" -t "$pkgdir/usr/share/applications/"

  # icons
  for size in 16x16 32x32 48x48 64x64 128x128; do
    install -Dm644 Browser/browser/chrome/icons/default/default${size/x*/}.png \
      "$pkgdir/usr/share/icons/hicolor/${size}/apps/${pkgname%-bin}.png"
  done
}
