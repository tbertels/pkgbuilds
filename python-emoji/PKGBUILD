# https://archlinux.org/packages/community/any/python-emoji/
pkgname=python-emoji
_name=${pkgname#python-}
pkgver=1.6.1
pkgrel=1
pkgdesc="Emoji for Python"
arch=('any')
url="https://github.com/carpedm20/emoji"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz")
sha512sums=('1d0ac39c68006ad844b4d601c0b992d73e6d60c3704e217666514bed7e694f62095cb81e1bfba8b817147d6f14425ec72ee6d687344ace2dc4c183026fa8c20d')

build() {
  cd "$_name-$pkgver"
  python setup.py build
}

check() {
  cd "$_name-$pkgver"
  pytest
}

package() {
  cd "$_name-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
