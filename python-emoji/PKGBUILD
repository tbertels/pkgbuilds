# https://archlinux.org/packages/community/any/python-emoji/
pkgname=python-emoji
_name=${pkgname#python-}
pkgver=1.5.0
pkgrel=1
pkgdesc="Emoji for Python"
arch=('any')
url="https://github.com/carpedm20/emoji"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz")
sha512sums=('aab01bd6a4e4156aaaa29ecd5b7ab261ba642c3718a31ca483ae5f9ba3a49c18f77566a3874847fc21a649bed76181d747e4966082e778aed6958cd3f7ad77d7')

build() {
  cd "$_name-$pkgver"
  python setup.py build
}

check() {
  cd "$_name-$pkgver"
  nosetests
}

package() {
  cd "$_name-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
