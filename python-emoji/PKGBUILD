# https://archlinux.org/packages/community/any/python-emoji/
pkgname=python-emoji
_name=${pkgname#python-}
pkgver=1.6.0
pkgrel=1
pkgdesc="Emoji for Python"
arch=('any')
url="https://github.com/carpedm20/emoji"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz")
sha512sums=('2c9cda590ae65a2fe0a2d7de8408902eb73ecd011a75976142079689a6169bc6487762af93192f99ebfbeb1b8fb6f55a5de7ad3186005c14baa829c6193c818e')

build() {
  cd "$_name-$pkgver"
  python setup.py build
}

check() {
  cd "$_name-$pkgver"
  pytest
}

package() {
  cd "$_name-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
