# https://archlinux.org/packages/community/any/python-dropbox/
pkgname=python-dropbox
_pkgname=dropbox-sdk-python
pkgver=11.7.0
pkgrel=1
pkgdesc="Python SDK for Dropbox Core APIs"
url="https://github.com/dropbox/dropbox-sdk-python"
arch=('any')
license=('MIT')
depends=(python python-urllib3 python-requests python-six python-stone)
makedepends=(python-setuptools)
#checkdepends=(python-pytest-runner)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dropbox/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('b80731870f9b76e5b98107faefd69f0b48d1467f46ddb7333e9bcde2a5ea78c08c87091e0305bf0518b536243787a798022b974d1f3cec7afdd713bc6ab9e5de')

prepare() {
	cd "$_pkgname-$pkgver"

	# Pytest Runner not required to build
	sed -i '/pytest-runner/d' setup.py
}

build() {
  cd "$_pkgname-$pkgver"
  python setup.py build
}

# requires API token in $DROPBOX_TOKEN
#check() {
#  cd "$_pkgname-$pkgver"
#
#  python setup.py pytest --addopts "-k 'not TestDropboxTeam'"

package() {
  cd "$_pkgname-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm0644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
