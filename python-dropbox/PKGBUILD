# https://archlinux.org/packages/community/any/python-dropbox/
groups=('modified')

pkgname=python-dropbox
_pkgname=dropbox-sdk-python
pkgver=11.23.0
pkgrel=1
pkgdesc="Python SDK for Dropbox Core APIs"
url="https://github.com/dropbox/dropbox-sdk-python"
arch=('any')
license=('MIT')
depends=(python python-urllib3 python-requests python-six python-stone)
makedepends=(python-setuptools python-pytest-runner)
#checkdepends=(python-pytest-mock python-mock python-coverage)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dropbox/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('119f91d166757f5d085d7acb9475a111f11ec0381f704045119b5636d76658a15546e27d5f14af3f262d10e3c3744ca125cc3589d45a36eaf0b8e8f13a43a999')

prepare() {
  cd "$_pkgname-$pkgver"
  sed -e 's|pytest-runner == 5.2.0|pytest-runner|' -i setup.py # Unversion dependency
}

build() {
  cd "$_pkgname-$pkgver"
  python setup.py build
}

# requires API token in $DROPBOX_TOKEN
#check() {
#  cd $_pkgname-$pkgver"
#  python setup.py pytest --addopts "-k 'not TestDropboxTeam'"
#}

package() {
  cd "$_pkgname-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm0644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
