# https://archlinux.org/packages/community/any/python-dropbox/
groups=('modified')

pkgname=python-dropbox
_pkgname=dropbox-sdk-python
pkgver=11.20.0
pkgrel=1
pkgdesc="Python SDK for Dropbox Core APIs"
url="https://github.com/dropbox/dropbox-sdk-python"
arch=('any')
license=('MIT')
depends=(python python-urllib3 python-requests python-six python-stone)
makedepends=(python-setuptools)
#checkdepends=(python-pytest-mock python-mock python-pytest-runner python-coverage)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dropbox/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('83400f3cb87a0273094522207400d756ff25f12e53dd12c55365618e97ca45593c5a5d1714dc66ffe455342c91e08c0826b0c20503602ae10261c778e607d98d')

prepare() {
  cd "$_pkgname-$pkgver"

  # Pytest Runner not required to build
  sed -i '/pytest-runner/d' setup.py
}

build() {
  cd "$_pkgname-$pkgver"
  python setup.py build
}

# requires API token in $DROPBOX_TOKEN
#check() {
#  cd $_pkgname-$pkgver"
#  python setup.py pytest --addopts "-k 'not TestDropboxTeam'"
#}

package() {
  cd "$_pkgname-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm0644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
