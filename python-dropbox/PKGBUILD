# https://archlinux.org/packages/community/any/python-dropbox/
pkgname=python-dropbox
_pkgname=dropbox-sdk-python
pkgver=11.6.0
pkgrel=1
pkgdesc="Python SDK for Dropbox Core APIs"
url="https://github.com/dropbox/dropbox-sdk-python"
arch=('any')
license=('MIT')
depends=(python python-urllib3 python-requests python-six python-stone)
makedepends=(python-setuptools)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dropbox/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('20199626ee9eee6d51e432a2d1434d8d21c3c5261a56f50d94fc642458a58bbc3f01f16d58c1b4373d189e51c62ffc09e08e2e1916b77699ffc83d891ce00270')

prepare() {
	cd "$_pkgname-$pkgver"

	# Pytest Runner not required to build
	sed -i '/pytest-runner/d' setup.py
}

build() {
  cd "$_pkgname-$pkgver"
  python setup.py build
}

# requires API token in $DROPBOX_TOKEN
#check() {
#  cd "$_pkgname-$pkgver"
#
#  python setup.py pytest --addopts "-k 'not TestDropboxTeam'"

package() {
  cd "$_pkgname-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm0644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
