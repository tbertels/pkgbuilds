# https://aur.archlinux.org/packages/icoextract
groups=('modified')

pkgname=icoextract
pkgver=0.1.4
pkgrel=2
pkgdesc="Thumbnailer for Windows PE files (.exe/.dll)"
arch=('any')
url="https://github.com/jlu5/icoextract"
license=('MIT')
depends=('python-pefile')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
optdepends=('python-pillow: required for the optional thumbnailer')
conflicts=('exe-thumbnailer')
_commit=182c3b34befebb7b3e949e878fc2ab8631864f0b  # tags/0.1.4^0
source=("git+https://github.com/jlu5/icoextract.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd "$pkgname"

  # Add missing MIME type (#19)
  git cherry-pick -n 31ce309a4b5ed345ede44bb1270ca06cc6f32aae
}

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 exe-thumbnailer.thumbnailer -t "$pkgdir/usr/share/thumbnailers/"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
