pkgname=planify-devel
_app_id=com.github.alainm23.planify
pkgver=3.0.7+41+g394e7bad
pkgrel=1
pkgdesc="Task manager with Todoist support (Development version)"
arch=('x86_64')
url="https://useplanner.com"
license=('GPL3')
depends=('evolution-data-server' 'granite7' 'gtksourceview5' 'libadwaita'
         'webkitgtk-6.0')
makedepends=('git' 'gobject-introspection' 'meson' 'vala')
checkdepends=('appstream')
provides=('planify')
conflicts=('planify' 'elementary-planner' 'task-planner')
replaces=('elementary-planner' 'task-planner)
_commit=394e7bada02b0ea4f89f4b006819e751565a5021  # branch/master
source=("git+https://github.com/alainm23/planner.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd planner
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson planner build
  meson compile -C build
}

check() {
  desktop-file-validate "build/data/${_app_id}.Devel.desktop"
  appstreamcli validate --no-net "build/data/${_app_id}.Devel.appdata.xml" || :
}

package() {
  meson install -C build --destdir "$pkgdir"

  ln -s "/usr/bin/${_app_id}" "$pkgdir/usr/bin/planify"
}
