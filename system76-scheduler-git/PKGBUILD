# https://aur.archlinux.org/packages/system76-scheduler-git
groups=('modified')

pkgname=system76-scheduler-git
pkgver=1.0.0.r0.gd508ff1
pkgrel=1
pkgdesc="Auto-configure CFS and process priorities for improved desktop responsiveness"
arch=('x86_64')
url="https://github.com/pop-os/system76-scheduler"
license=('MPL2')
depends=('gcc-libs')
makedepends=('cargo' 'git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
backup=("etc/${pkgname%-git}/config.ron"
        "etc/${pkgname%-git}/assignments/default.ron")
source=('git+https://github.com/pop-os/system76-scheduler.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/${pkgname%-git}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}


build() {
  cd "$srcdir/${pkgname%-git}"
  export RUSTUP_TOOLCHAIN=stable
  make
}

package() {
  cd "$srcdir/${pkgname%-git}"
  make DESTDIR="$pkgdir" install
}
