# https://archlinux.org/packages/community/any/python-debian
groups=('modified')

pkgname=python-debian
pkgver=0.1.40
pkgrel=1
pkgdesc="Python modules to work with Debian-related data formats"
arch=('any')
url="https://salsa.debian.org/python-debian-team/python-debian"
license=('GPL2')
depends=('python-chardet' 'python-six')
makedepends=('python-setuptools')
optdepends=('python-apt: some modules can use it to speed up processing')
source=("https://pypi.org/packages/source/${pkgname:0:1}/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('385dfb965eca75164d256486c7cf9bae772d24144249fd18b9d15d3cffb70eea'
            'SKIP')
validpgpkeys=('90E2D2C1AD146A1B7EBB891DBBC17EBB1396F2F7') # Stuart Prescott <stuart@nanonanonano.net>

build() {
	cd "$pkgname-$pkgver"
	python setup.py build
}

check() {
	cd "$pkgname-$pkgver"
#	rm lib/debian/tests/test_deb{822,file}.py # remove failing tests .. for now
	python -m unittest discover --verbose lib/
}

package() {
	cd "$pkgname-$pkgver"
	export PYTHONHASHSEED=0
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
