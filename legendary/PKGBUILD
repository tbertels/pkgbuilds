# https://aur.archlinux.org/packages/legendary
groups=('modified')

pkgname=legendary
pkgver=0.20.34
pkgrel=1
pkgdesc="A free and open-source replacement for the Epic Games Launcher"
arch=('any')
url="https://github.com/derrod/legendary"
license=('GPL3')
depends=('python-filelock' 'python-requests')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
optdepends=('python-pywebview: webview-based login')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('f0f31f1f09422901eaf71e4e8c1b873c80e1640236041fd05932eab656535b58')

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
