# https://aur.archlinux.org/packages/legendary
groups=('modified')

pkgname=legendary
pkgver=0.20.33
pkgrel=2
pkgdesc="A free and open-source replacement for the Epic Games Launcher"
arch=('any')
url="https://github.com/derrod/legendary"
license=('GPL3')
depends=('python-filelock' 'python-requests')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
optdepends=('python-pywebview: webview-based login')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('b20b190b6cccbde46f394eb970c30e056f01aa1bf44eb936c927cb38926bd150')

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
