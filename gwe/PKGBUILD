# https://aur.archlinux.org/packages/gwe
groups=('modified')

pkgname=gwe
pkgver=0.15.5
pkgrel=4
pkgdesc="A system utility for controlling NVIDIA GPUs"
arch=('any')
url="https://gitlab.com/leinardi/gwe"
license=('GPL3')
depends=('gtk3' 'libdazzle' 'libnotify' 'python' 'python-cairo' 'python-gobject'
         'python-injector' 'python-matplotlib' 'python-peewee' 'python-py3nvml'
         'python-reactivex' 'python-requests' 'python-rx' 'python-pyxdg' 'python-xlib')
makedepends=('git' 'gobject-introspection' 'meson')
optdepends=('libappindicator-gtk3: Show App Indicator')
_commit=21ca1e352f69de40477328c8e0bfd6ebed24ab6c  # tags/0.15.5^0
source=("git+https://gitlab.com/leinardi/gwe.git#commit=${_commit}"
        'issue-206.patch')
sha256sums=('SKIP'
            '9c5a6fae174a1ddd85c2db8f6bbbfe30421fa2fca9d4832f5646e850d3c50c35')

pkgver() {
  cd "$pkgname"
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd "$pkgname"

  # https://gitlab.com/leinardi/gwe/-/issues/206
  # https://gitlab.com/leinardi/gwe/-/merge_requests/76
  patch -Np1 -i ../issue-206.patch
}

build() {
  arch-meson "$pkgname" build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
