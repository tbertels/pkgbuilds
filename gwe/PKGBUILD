# https://aur.archlinux.org/packages/gwe
groups=('modified')

pkgname=gwe
pkgver=0.15.6
pkgrel=1
pkgdesc="A system utility for controlling NVIDIA GPUs"
arch=('any')
url="https://gitlab.com/leinardi/gwe"
license=('GPL3')
depends=('gtk3' 'libdazzle' 'libnotify' 'python' 'python-cairo' 'python-gobject'
         'python-injector' 'python-matplotlib' 'python-peewee' 'python-py3nvml'
         'python-reactivex' 'python-requests' 'python-rx' 'python-pyxdg' 'python-xlib')
makedepends=('git' 'gobject-introspection' 'meson')
optdepends=('libappindicator-gtk3: Show App Indicator')
_commit=c3f99cee13bc5df787189da00196e575bda37856  # tags/0.15.6^0
source=("git+https://gitlab.com/leinardi/gwe.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd "$pkgname"
}

build() {
  arch-meson "$pkgname" build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
