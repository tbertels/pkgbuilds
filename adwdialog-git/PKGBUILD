# https://aur.archlinux.org/packages/adwdialog-git
groups=('modified')

pkgname=adwdialog-git
pkgver=r11.20a17be
pkgrel=1
pkgdesc="Display GTK4/libadwaita dialogs from terminal and scripts."
arch=('any')
url="https://github.com/Vanilla-OS/AdwDialog"
license=('GPL3')
depends=('libadwaita' 'libjpeg-turbo' 'python-gobject')
makedepends=('git' 'meson')
checkdepends=('appstream-glib')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/Vanilla-OS/AdwDialog.git')
sha256sums=('SKIP')

pkgver() {
  cd AdwDialog
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  arch-meson AdwDialog build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"
}
