# http://aur.archlinux.org/packages/scrcpy
groups=('modified')

pkgname=scrcpy
pkgver=1.18
pkgrel=1
pkgdesc="Display and control your Android device"
arch=('i686' 'x86_64')
url="https://github.com/Genymobile/scrcpy"
license=('Apache')
depends=('android-tools' 'ffmpeg' 'sdl2')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
noextract=("$pkgname-server"*)
sha256sums=('2995d74409e9a486e4f69d0f623299ebf615d9427d8e974dfd82355538a313e9'
            '641c5c6beda9399dfae72d116f5ff43b5ed1059d871c9ebc3f47610fd33c51a3')

build() {
	arch-meson \
		-Dprebuilt_server="../$pkgname-server-v$pkgver" \
		"$pkgname-$pkgver" build
	meson compile -C build
}

# No tests defined
#check() {
#	meson test -C build --print-errorlogs
#}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
