# http://aur.archlinux.org/packages/scrcpy
groups=('modified')

pkgname=scrcpy
pkgver=1.19
pkgrel=1
pkgdesc="Display and control your Android device"
arch=('i686' 'x86_64')
url="https://github.com/Genymobile/scrcpy"
license=('Apache')
depends=('android-tools' 'ffmpeg' 'sdl2')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
noextract=("$pkgname-server"*)
sha256sums=('40ac2059618f18a0e6f9cd0e40d3bff240ade98664bd619422f875bff7e8c70e'
            '876f9322182e6aac6a58db1334f4225855ef3a17eaebc80aab6601d9d1ecb867')

build() {
  arch-meson "$pkgname-$pkgver" build \
    -Dprebuilt_server="../$pkgname-server-v$pkgver"
  meson compile -C build
}

# No tests defined
#check() {
#  meson test -C build --print-errorlogs
#}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
