# http://aur.archlinux.org/packages/scrcpy
groups=('modified')

pkgname=scrcpy
pkgver=1.17
pkgrel=1.1
pkgdesc='Display and control your Android device'
arch=('i686' 'x86_64')
url='https://github.com/Genymobile/scrcpy'
license=('Apache')
depends=('android-tools' 'ffmpeg' 'sdl2')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
noextract=("$pkgname-server"*)
sha256sums=('c16f1fe1789290d0c7dd3a2778f33d6dc6347ffe3e78e64127b85eda52420d7f'
            '11b5ad2d1bc9b9730fb7254a78efd71a8ff46b1938ff468e47a21b653a1b6725')

build() {
    arch-meson \
        -Dprebuilt_server="../$pkgname-server-v$pkgver" \
        "$pkgname-$pkgver" build
    meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
