# https://aur.archlinux.org/packages/pipecontrol
groups=('modified')

pkgname=pipecontrol
pkgver=0.2.10
pkgrel=1
pkgdesc="Pipewire control GUI program in Qt"
arch=('x86_64' 'aarch64')
url="https://github.com/portaloffreedom/pipecontrol"
license=('GPL3')
makedepends=('appstream' 'cmake' 'extra-cmake-modules' 'qt5-tools')
depends=('hicolor-icon-theme' 'kcoreaddons' 'ki18n' 'kirigami2' 'libpipewire')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('a101d96a66c87340d615b6a400e0af072fab9383498506826822c2defac84d20')

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
