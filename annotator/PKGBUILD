# https://aur.archlinux.org/packages/annotator
groups=('modified')

pkgname=annotator
_app_id=com.github.phase1geo.annotator
pkgver=1.2.0
pkgrel=1
pkgdesc="Image annotation for Elementary OS"
arch=('x86_64')
url="https://github.com/phase1geo/Annotator"
license=('GPL3')
depends=('granite' 'libhandy' 'python-gobject')
makedepends=('meson' 'vala')
checkdepends=('appstream')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('8b50f3d304b6c19383508c09719c3b6a817e9178767f4567b9d0613c864840b8')

build() {
  arch-meson "Annotator-$pkgver" build
  meson compile -C build
}

check() {
  desktop-file-validate "build/data/${_app_id}.desktop"
  appstreamcli validate "build/data/${_app_id}.appdata.xml" || :
}

package() {
  meson install -C build --destdir "$pkgdir"

  ln -s "/usr/bin/${_app_id}" "$pkgdir/usr/bin/$pkgname"
}
