pkgname=gnome-shell-extension-apps-to-panel-git
pkgver=r18.0bf41ee
pkgrel=1
pkgdesc="Turns the Activities button into two buttons with icon or text to select Applications or Workspaces in the Overview."
arch=('any')
url="https://github.com/aunetx/apps-to-panel"
license=('unknown')
depends=('gnome-shell')
makedepends=('git' 'meson')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/aunetx/apps-to-panel.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/apps-to-panel"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	arch-meson apps-to-panel build
	meson compile -C build
}

# No tests defined
#check() {
#	meson test -C build --print-errorlogs
#}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
