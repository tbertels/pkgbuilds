# https://aur.archlinux.org/packages/opencl-caps-viewer
groups=('modified')

pkgname=opencl-caps-viewer-wayland
pkgver=1.10
pkgrel=1
pkgdesc="OpenCL Hardware Capability Viewer"
arch=('x86_64')
url="https://opencl.gpuinfo.org/"
license=('GPL2')
depends=('opencl-icd-loader' 'qt5-wayland')
makedepends=('git')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
_commit=0fe6301a2346f8f8bc0989db4be9839f47054aee  # tags/1.10^
source=("git+https://github.com/SaschaWillems/OpenCLCapsViewer.git#commit=${_commit}"
        'git+https://github.com/KhronosGroup/OpenCL-Headers.git')
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd "$srcdir/OpenCLCapsViewer"
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd "$srcdir/OpenCLCapsViewer"
  git submodule init
  git config submodule.OpenCL-Headers.url "$srcdir/OpenCL-Headers"
  git submodule update

  # Correct binary install path
  sed -i 's|target.path = /opt/$${TARGET}/bin|target.path = /usr/bin|g' OpenCLCapsViewer.pro
}

build() {
  cd "$srcdir/OpenCLCapsViewer"
  qmake-qt5 OpenCLCapsViewer.pro \
    DEFINES+=WAYLAND \
    CONFIG+=release \
    PREFIX=/usr
  make
}

package() {
  cd "$srcdir/OpenCLCapsViewer"
  make INSTALL_ROOT="$pkgdir/" install

  install -m644 Resources/icon.png "${pkgdir}"/usr/share/icons/hicolor/256x256/apps/openclCapsViewer.png
}
