pkgname=savedesktop
_app_id=io.github.vikdevelop.SaveDesktop
pkgver=2.3
pkgrel=1
pkgdesc="Save and load KDE Plasma, Xfce and GNOME-based DE configuration"
arch=('any')
url="https://github.com/vikdevelop/SaveDesktop"
license=('GPL3')
depends=('hicolor-icon-theme' 'libadwaita' 'python-gobject')
checkdepends=('appstream-glib')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('1cb26f1b99d3dda315a43a4ba102e1910e6bb5af3b2a9c9d30f55c48ae31d736')

prepare() {
  cd "SaveDesktop-$pkgver"
  sed -i "s|Exec=run.sh|Exec=/usr/share/$pkgname/main_window.py|g" \
    "flatpak/$_app_id.desktop"
  sed -i "s|/app/translations/|/usr/share/$pkgname/translations/|g" \
    src/main_window.py
#  sed -i 's|dialog.set_version("2.2.4")|dialog.set_version("2.2.5")|g' \
#    src/main_window.py
}

check() {
  cd "SaveDesktop-$pkgver"
  appstream-util validate-relax --nonet "flatpak/$_app_id.metainfo.xml"
  desktop-file-validate "flatpak/$_app_id.desktop"
}

package() {
  cd "SaveDesktop-$pkgver"
  install -Dm755 src/main_window.py -t "$pkgdir/usr/share/$pkgname/"
  cp -R translations "$pkgdir/usr/share/$pkgname/"
  install -Dm644 "flatpak/$_app_id.desktop" -t "$pkgdir/usr/share/applications/"
  install -Dm644 "flatpak/icons/$_app_id.png" -t "$pkgdir/usr/share/pixmaps/"
  install -Dm644 flatpak/symbolic-icons/exclamation_mark.png -t \
    "$pkgdir/usr/share/icons/hicolor/symbolic/actions/"
  install -Dm644 "flatpak/$_app_id.metainfo.xml" -t "$pkgdir/usr/share/metainfo/"

  install -d "$pkgdir/usr/bin"
  ln -s "/usr/share/$pkgname/main_window.py" "$pkgdir/usr/bin/$pkgname"
}
