# https://aur.archlinux.org/packages/gittyup
groups=('modified')

pkgname=gittyup
pkgver=1.1.1
pkgrel=1
pkgdesc="A graphical Git client designed to help you understand and manage your source code history."
arch=('x86_64')
url="https://murmele.github.io/Gittyup"
license=('MIT')
depends=('cmark' 'hunspell' 'libgit2' 'libsecret' 'lua' 'qt5-base')
makedepends=('cmake' 'git' 'qt5-tools') # 'http-parser'

# http-parser version 2 was not found or disabled; using bundled 3rd-party sources

optdepends=('git: needed for the credential helpers'
            'git-lfs: git LFS support')
_commit=57d7aff03c9337db6f28217d5e865eafd1c191f5
source=("git+https://github.com/Murmele/Gittyup.git#commit=$_commit"
#        'git+https://github.com/commonmark/cmark.git'
#        'git+https://github.com/git/git.git'
#        'git+https://github.com/hunspell/hunspell'
#        'git+https://github.com/stinb/libgit2.git'
#        'git+https://github.com/libssh2/libssh2.git'
#        'git+https://github.com/openssl/openssl.git'
        'git+https://github.com/kuba--/zip.git'
        )
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd "$srcdir/Gittyup"
  git describe --tags | sed "s/^${pkgname}_v//;s/-/+/g"
}

prepare() {
  cd "$srcdir/Gittyup"
  git submodule init

  git config submodule.dep/zip/zip.url "$srcdir/zip"

  for submodule in cmark hunspell libgit2 libssh2 openssl; do
    git -c submodule.dep/${submodule}/${submodule}.update=none submodule update
  done

  sed -i -e 's/cmark_exe/cmark/' src/app/CMakeLists.txt
}

build() {
  cmake -B build -S Gittyup \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DBUILD_SHARED_LIBS='OFF' \
    -DENABLE_REPRODUCIBLE_BUILDS='ON' \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$srcdir/Gittyup"
  install -Dm644 LICENSE.md -t  "$pkgdir/usr/share/licenses/$pkgname/"
  install -Dm644 "rsrc/linux/com.github.Murmele.Gittyup.desktop" -t \
    "$pkgdir/usr/share/applications/"
  install -Dm644 "rsrc/linux/com.github.Murmele.Gittyup.appdata.xml.in" \
    "$pkgdir/usr/share/appdata/com.github.Murmele.Gittyup.appdata.xml"

  for s in 16x16 32x32 64x64 128x128 256x256 512x512; do
    install -Dm644 rsrc/Gittyup.iconset/icon_${s}.png \
      "$pkgdir/usr/share/icons/hicolor/${s}/apps/$pkgname.png"
  done

  install -d "$pkgdir/usr/"{share/${pkgname},bin}
  mv "$pkgdir/usr/"{git-credential-libsecret,Gittyup,indexer,relauncher} \
    "$pkgdir/usr/share/$pkgname/"
  mv "$pkgdir"/usr/{Plugins,Resources} "$pkgdir/usr/share/$pkgname/"
  ln -s "/usr/share/$pkgname/Gittyup" "$pkgdir/usr/bin/"

  # Remove shared files
  rm -f "$pkgdir/usr/"*.so.*
  rm -rf "$pkgdir/usr/"{include,lib}

  # Remove libssh2 docs & man pages
  rm -rf "$pkgdir"/usr/share/{doc,man}
}
