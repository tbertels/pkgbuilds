# https://aur.archlinux.org/packages/gittyup
groups=('modified')

pkgname=gittyup
pkgver=1.2.0
pkgrel=1
pkgdesc="A graphical Git client designed to help you understand and manage your source code history."
arch=('x86_64')
url="https://murmele.github.io/Gittyup"
license=('MIT')
depends=('cmark' 'hicolor-icon-theme' 'hunspell' 'libgit2' 'libsecret' 'lua'
         'pcre' 'qt5-base')
makedepends=('cmake' 'git' 'qt5-tools')
optdepends=('git: needed for the credential helpers'
            'git-lfs: git LFS support')
_commit=b663589ddc5c0267a039de9448646bb1c7f7f7f5  # tags/gittyup_v1.2.0^0
source=("git+https://github.com/Murmele/Gittyup.git#commit=$_commit"
        'git+https://github.com/commonmark/cmark.git'
        'git+https://github.com/git/git.git'
        'git+https://github.com/hunspell/hunspell'
        'git+https://github.com/stinb/libgit2.git'
        'git+https://github.com/libssh2/libssh2.git'
        'git+https://github.com/openssl/openssl.git'
        'git+https://github.com/ScintillaOrg/lexilla.git'
        'git+https://github.com/orbitalquark/scintillua.git'
        'git+https://github.com/kuba--/zip.git'
        )
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd "$srcdir/Gittyup"
  git describe --tags | sed "s/^${pkgname}_v//;s/-/+/g"
}

prepare() {
  cd "$srcdir/Gittyup"

  _submodules=(
    cmark
    git
    hunspell
    lexilla
    libgit2
    libssh2
    openssl
    scintillua
    zip
  )

  git submodule init

  for submodule in "${_submodules[@]}"; do
    git config "submodule.dep/${submodule#*::}/${submodule#*::}.url" \
      "$srcdir/${submodule%::*}"
  done

  git -c protocol.file.allow=always submodule update

  sed -i -e 's/cmark_exe/cmark/' src/app/CMakeLists.txt
}

build() {

  # prevent static lib mangling with LTO
  CFLAGS+=" -ffat-lto-objects"

  cmake -B build -S Gittyup \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DBUILD_SHARED_LIBS='OFF' \
    -DENABLE_REPRODUCIBLE_BUILDS='ON' \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  _app_id='com.github.Murmele.Gittyup'

  cd "$srcdir/Gittyup"
  mv "$pkgdir/usr/"{git-credential-libsecret,Gittyup,indexer,relauncher} \
    "$pkgdir/usr/share/$pkgname/"
  mv "$pkgdir"/usr/Plugins "$pkgdir/usr/share/$pkgname/"
  cp -r "$pkgdir"/usr/Resources/* "$pkgdir/usr/share/$pkgname/Resources/"
  rm -rf "$pkgdir"/usr/Resources
  ln -s "/usr/share/$pkgname/Gittyup" "$pkgdir/usr/bin/"
  
  install -Dm644 "rsrc/linux/${_app_id}.desktop" -t \
    "$pkgdir/usr/share/applications/"
  install -Dm644 "rsrc/linux/${_app_id}.appdata.xml.in" \
    "$pkgdir/usr/share/appdata/${_app_id}.appdata.xml"

  for s in 16x16 32x32 64x64 128x128 256x256 512x512; do
    install -Dm644 rsrc/Gittyup.iconset/icon_${s}.png \
      "$pkgdir/usr/share/icons/hicolor/${s}/apps/$pkgname.png"
  done

  install -Dm644 LICENSE.md -t  "$pkgdir/usr/share/licenses/$pkgname/"

  # Remove shared files
  rm -rf "$pkgdir/usr/bin/cmark"
  rm -f "$pkgdir/usr/"*.so.*
  rm -rf "$pkgdir/usr/"{include,lib}

#  # Remove cmark & libssh2 man pages
  rm -rf "$pkgdir"/usr/share/man
}
