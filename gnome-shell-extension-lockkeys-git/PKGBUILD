# https://aur.archlinux.org/packages/gnome-shell-extension-lockkeys-git
groups=('modified')

pkgname=gnome-shell-extension-lockkeys-git
pkgver=r152.d4c6be1
pkgrel=1
pkgdesc="Numlock and Capslock indicator for gnome-shell"
arch=('any')
url="https://github.com/kazysmaster/gnome-shell-extension-lockkeys"
license=('none')
depends=('gnome-shell' 'hicolor-icon-theme')
makedepends=('git')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("${pkgname%-*}::git+$url.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-*}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	_uuid='lockkeys@vaina.lt'

	cd "$srcdir/${pkgname%-*}/$_uuid"
	install -Dm644 icons/*.svg -t "$pkgdir/usr/share/icons/hicolor/symbolic/apps"
	install -Dm644 schemas/*.xml -t "$pkgdir/usr/share/glib-2.0/schemas"

	for locale in locale/*/; do
		install -Dm644 -t "$pkgdir/usr/share/${locale}/LC_MESSAGES" \
			"${locale}/LC_MESSAGES"/*.mo
	done

	rm -rf {icons,locale}

	cd "$srcdir/${pkgname%-*}"
	install -d "$pkgdir/usr/share/gnome-shell/extensions"
	cp -r $_uuid "$pkgdir/usr/share/gnome-shell/extensions"
}
