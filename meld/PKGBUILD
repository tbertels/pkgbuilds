# https://archlinux.org/packages/extra/any/meld/
groups=('modified')

pkgname=meld
pkgver=3.22.0+r278+g704e86c4
pkgrel=1
pkgdesc="Compare files, directories and working copies"
url="https://meldmerge.org/"
license=(GPL)
arch=(any)
depends=(dconf gsettings-desktop-schemas gtksourceview5 libadwaita python-cairo
         python-distro python-gobject)
makedepends=(git gtksourceview4 meson yelp-tools)
checkdepends=(appstream-glib)
source=('git+https://gitlab.gnome.org/philippun1/meld.git#branch=gtk4')
sha256sums=('SKIP')

pkgver() {
  cd meld
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd meld
}

build() {
  arch-meson meld build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
