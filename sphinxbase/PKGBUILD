# https://aur.archlinux.org/packages/sphinxbase
groups=('modified')

pkgname=sphinxbase
pkgver=5prealpha
pkgrel=12
pkgdesc='Common library for sphinx speech recognition.'
arch=('x86_64')
url="https://cmusphinx.github.io"
license=('BSD')
depends=('python' 'lapack' 'libpulse')
makedepends=('swig')
provides=('_sphinxbase.so=0' 'libsphinxbase.so=3' 'libsphinxad.so=3')
source=("https://downloads.sourceforge.net/project/cmusphinx/sphinxbase/$pkgver/$pkgname-$pkgver.tar.gz"
        "$pkgname-5prealpha-fix-doxy2swig.patch")
sha256sums=('f72bdb59e50b558bed47cc2105777200d2b246a0f328e913de16a9b22f9a246f'
            '1cb485202f83dc517872f5ab41f59d18884af1b85799166d80e08860f7729919')

prepare() {
  cd "$pkgname-$pkgver"

  # Reconfigure project for current version of Automake
  autoreconf -ivf > /dev/null

  patch -p1 -b -i "$srcdir/$pkgname-5prealpha-fix-doxy2swig.patch"
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
