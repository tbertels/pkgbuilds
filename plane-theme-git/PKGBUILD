# https://gitlab.manjaro.org/packages/community/themes/plane-theme
# https://aur.archlinux.org/packages/plane-theme-git
pkgname=plane-theme-git
pkgver=r179.a11a397
pkgrel=1
pkgdesc='A beautiful GTK theme for GNOME and Xfce'
arch=('any')
url="https://www.pling.com/p/1198768"
license=('GPL')
makedepends=('git' 'npm' 'nvm' 'gulp')
optdepends=('plane-icon-theme: matching icon theme'
            'breeze-snow-cursor-theme: recommended cursor theme')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=('!strip')
#source=("git+https://github.com/wfpaisa/plane-theme.git")
source=("git+https://github.com/vantu5z/plane-theme.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

_ensure_local_nvm() {
	# lets be sure we are starting clean
	which nvm >/dev/null 2>&1 && nvm deactivate && nvm unload
	export NVM_DIR="$srcdir/.nvm"

	# The init script returns 3 if version
	# specified in ./.nvrc is not (yet) installed in $NVM_DIR
	# but nvm itself still gets loaded ok
	source /usr/share/nvm/init-nvm.sh || [[ $? != 1 ]]
}

prepare() {
	export npm_config_cache="$srcdir/npm-cache"
	npm config delete prefix
	source /usr/share/nvm/init-nvm.sh
	local npm_prefix=$(npm config get prefix)
	local nodeversion='12.22.1'
	_ensure_local_nvm
	nvm install "$nodeversion" && nvm use "$nodeversion"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	npm install
	gulp

	# Restore node config from nvm
	npm config set prefix "$npm_prefix"
	nvm unalias default
}

package() {
	install -d "$pkgdir/usr/share/themes"

	cd "$srcdir/${pkgname%-git}/build"
	cp -r Plane Plane-dark "$pkgdir/usr/share/themes"
}
