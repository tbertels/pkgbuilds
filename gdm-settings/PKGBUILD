# https://aur.archlinux.org/packages/gdm-settings
groups=('modified')

pkgname=gdm-settings
pkgver=3.0
pkgrel=1
pkgdesc="A settings app for GNOME's Login Manager, GDM"
arch=('any')
url="https://realmazharhussain.github.io/gdm-settings"
license=('AGPL3')
depends=('gettext' 'libadwaita' 'python-gobject')
makedepends=('blueprint-compiler' 'meson')
checkdepends=('appstream')
source=("$pkgname-$pkgver.tar.gz::https://github.com/realmazharhussain/gdm-settings/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('e4b33b8506383fea7bda7dd354183bc18e19355080cd0f4899d973a43717332f')

build() {
  arch-meson "$pkgname-$pkgver" build --buildtype release
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir="$pkgdir"
}
