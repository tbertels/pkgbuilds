# https://aur.archlinux.org/packages/gdm-settings
groups=('modified')

pkgname=gdm-settings
pkgver=2.1
pkgrel=1
pkgdesc="A settings app for GNOME's Login Manager, GDM"
arch=('any')
url="https://realmazharhussain.github.io/gdm-settings"
license=('AGPL3')
depends=('gettext' 'libadwaita' 'python-gobject')
makedepends=('blueprint-compiler' 'meson')
checkdepends=('appstream')
source=("$pkgname-$pkgver.tar.gz::https://github.com/realmazharhussain/gdm-settings/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('d00f5294d90ab755f8a93697d75fec119c1f874aca98d1f65fdbcf29e2cc774e')

build() {
  arch-meson "$pkgname-$pkgver" build --buildtype=release
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir="$pkgdir"
}
