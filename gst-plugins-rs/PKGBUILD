# https://aur.archlinux.org/packages/gst-plugins-rs
groups=('modified')

#  --- stderr
#  thread 'main' panicked at /build/gst-plugins-rs/src/cargo-home/registry/src/index.crates.io-6f17d22bba15001f/dav1d-sys-0.7.2/build.rs:83:10:
#  called `Result::unwrap()` on an `Err` value: `PKG_CONFIG_ALLOW_SYSTEM_CFLAGS="1" PKG_CONFIG_ALLOW_SYSTEM_LIBS="1" PKG_CONFIG_PATH="/build/gst-plugins-rs/src/build/meson-uninstalled" "/usr/bin/pkg-config" "--libs" "--cflags" "dav1d" "dav1d >= 1.0.0" "dav1d <= 1.2.1"` did not exit successfully: exit status: 1
#  error: could not find system library 'dav1d' required by the 'dav1d-sys' crate

#  --- stderr
#  Package dependency requirement 'dav1d <= 1.2.1' could not be satisfied.
#  Package 'dav1d' has version '1.3.0', required version is '<= 1.2.1'

pkgname=gst-plugins-rs
pkgver=1.22.10
pkgrel=1
pkgdesc="GStreamer plugins written in Rust"
arch=('x86_64')
url="https://gitlab.freedesktop.org/gstreamer/gst-plugins-rs"
license=('Apache-2.0 AND LGPL-2.1-or-later AND MIT AND MPL-2.0')
depends=('cairo' 'dav1d' 'gst-plugins-base-libs' 'gst-plugins-bad-libs' 'graphene'
         'gstreamer' 'gtk4' 'libwebp' 'libsodium' 'openssl' 'pango')
makedepends=('cargo' 'cargo-c' 'clang' 'git' 'hotdoc' 'meson' 'nasm' 'python-tomli')
provides=('gst-plugin-gtk4')
conflicts=('gst-plugin-gtk4')
options=('!lto')
_commit=a84bbc66f30573b62871db163c48afef75adf6ec  # tags/gstreamer-1.22.10^0
source=("git+https://gitlab.freedesktop.org/gstreamer/gst-plugins-rs.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  git describe --tags | sed 's/^gstreamer-//;s/-/+/g'
}

prepare() {
  cd "${pkgname}"
  export CARGO_HOME="${srcdir}/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target x86_64-unknown-linux-gnu
}

build() {
  export CARGO_HOME="${srcdir}/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  arch-meson "${pkgname}" build \
    -D sodium-source='system'
  meson compile -C build
}

check() {
  export CARGO_HOME="${srcdir}/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "${pkgdir}"

  cd "${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
