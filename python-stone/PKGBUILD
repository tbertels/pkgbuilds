pkgname=python-stone
_name=${pkgname#python-}
pkgver=3.2.1
pkgrel=4
pkgdesc="The Official API Spec Language for Dropbox API V2"
arch=('any')
url="https://github.com/dropbox/stone"
license=('MIT')
depends=('python' 'python-ply' 'python-six')
makedepends=('python-setuptools' 'python-pytest-runner')
checkdepends=('python-mock' 'python-coverage' 'python-pytest')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('05a6c9fec0b923a5e54814f31365499d6e7b726aaddfe5b8ee2d1a4b6cb06d3a')
validpgpkeys=()

build() {
  cd "$_name-$pkgver"
  python setup.py build
}

check() {
  cd "$_name-$pkgver"
  pytest
}

package() {
  cd "$_name-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
