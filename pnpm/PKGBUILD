# https://aur.archlinux.org/packages/pnpm

pkgname=pnpm
pkgver=7.0.1
pkgrel=1
pkgdesc="Fast, disk space efficient package manager"
arch=('any')
url="https://pnpm.io"
license=('MIT')
depends=('nodejs>=12.17.0' 'python')
makedepends=('jq' 'npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha256sums=('ae0d84c718ec141f7419c23a43bc4c1f42114e846d9af3042826f8d6a553d711')

package() {
  npm install -g --prefix "$pkgdir"/usr "$srcdir/$pkgname-$pkgver.tgz"

  # npm gives ownership of ALL FILES to build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"

  # Sort and cleanup package.json
  find "$pkgdir" -name package.json -print0 | xargs -r -0 sed -i '/_where/d'

  local tmppackage="$(mktemp)"
  local npmdir="$pkgdir/usr/lib/node_modules/$pkgname"
  local pkgjson="$npmdir/package.json"
  jq '.|=with_entries(select(.key|test("_.+")|not))' "$pkgjson" > "$tmppackage"
  mv "$tmppackage" "$pkgjson"
  chmod 644 "$pkgjson"

  # Delete JS SourceMaps
  find "$pkgdir/usr/lib" -depth -name '*.map' -delete

  install -Dm644 "$npmdir/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -Dm644 "$npmdir/README.md" -t "$pkgdir/usr/share/doc/$pkgname/"
  rm -rf "$pkgdir/usr/etc" "$npmdir/LICENSE" "$npmdir/README.md"
}
