# https://aur.archlinux.org/packages/pyinstaller-hooks-contrib
groups=('modified')

pkgname=pyinstaller-hooks-contrib
pkgver=2024.0
pkgrel=1
pkgdesc="Community maintained hooks for PyInstaller"
arch=('any')
url="https://github.com/pyinstaller/pyinstaller-hooks-contrib"
license=('Apache-2.0' 'GPL-2.0-or-later')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('pyinstaller' 'python-psutil' 'python-pytest-timeout' 'python-pytest-xdist')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('cd21415348012e4f7a39bccc6de8f2f43bb10c031d5371d974b453f31d5fa7a2')

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname-$pkgver"
  pytest --maxfail=3 \
    -m "not darwin and not win32 and not slow" \
    -n=auto --maxprocesses="${PYTEST_XDIST_AUTO_NUM_WORKERS:-2}"
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
