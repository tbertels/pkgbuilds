# https://aur.archlinux.org/packages/hydrapaper-git
groups=('modified')

pkgname=hydrapaper-git
pkgver=2.0.3.r51.gb3fa43e
pkgrel=1
pkgdesc="A Gtk utility to set different backgrounds for each monitor on GNOME"
arch=('any')
url="https://gitlab.gnome.org/GabMus/HydraPaper"
license=('GPL3')
depends=('gtk4' 'python-pillow' 'libadwaita' 'python-gobject')
makedepends=('git' 'meson' 'gobject-introspection' 'pandoc')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+https://gitlab.gnome.org/GabMus/HydraPaper.git")
sha256sums=('SKIP')

prepare() {
	cd "$srcdir/${pkgname%-git}"
	sed -i "s/dependency('gtk+-3.0', version :'>=3.20')/dependency('gtk4', version :'>=4.0')/g" data/meson.build
}

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags --always | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	arch-meson "${pkgname%-git}" build
	meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
