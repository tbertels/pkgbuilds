# https://aur.archlinux.org/packages/gnome-shell-extension-gsconnect-git
groups=('modified')

pkgname=gnome-shell-extension-gsconnect-git
pkgver=48+12+g8e85f0fc
pkgrel=1
pkgdesc="KDE Connect implementation with GNOME Shell integration"
arch=('any')
url="https://github.com/andyholmes/gnome-shell-extension-gsconnect"
license=('GPL')
depends=('gnome-shell')
makedepends=('git' 'meson')
optdepends=(
  'evolution-data-server: access contacts in GNOME Online Accounts, Evolution or the local address book'
  'python-nautilus: share files from Nautilus'
  'gsound: sound effects'
)
#checkdepends=('appstream' 'eslint')
checkdepends=('appstream')
optdepends=(
  'evolution-data-server: access contacts in GNOME Online Accounts, Evolution or the local address book'
  'python-nautilus: share files from Nautilus'
  'nemo-python: share files from Nemo'
  'gsound: sound effects'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/andyholmes/gnome-shell-extension-gsconnect.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  arch-meson "${pkgname%-git}" build \
    -Dinstalled_tests=false \
    -Dfirewalld=true \
    -Dnemo=true
  meson compile -C build
}

check() {

  # MprisComponent test fails, only run standard tests
  meson test -C build 'Validate metainfo file' 'Validate desktop file' 'Validate desktop file (Preferences)' --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
