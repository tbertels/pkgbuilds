pkgname=manjaro-starter
pkgver=r36.2e40d4f
pkgrel=1
pkgdesc="A tool providing access to documentation and support for new Manjaro users."
arch=('x86_64' 'aarch64')
url="https://github.com/oguzkaganeren/manjaro-starter"
license=('unknown')
depends=('gtk3' 'manjaro-settings-manager' 'webkit2gtk')
makedepends=('cargo' 'git' 'node-gyp' 'yarn')
options=('!lto')
_commit=2e40d4fc13c48f697e8db6b433418c5ee4540b94
source=("git+https://github.com/oguzkaganeren/manjaro-starter.git#commit=$_commit"
        "$pkgname.desktop")
sha256sums=('SKIP'
            '6816202f8e7cf5c1f2f2bd2c53421c9a97b47f3af3fab9c2dd5d47e31635d0d0')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$pkgname"
  mkdir -p build

  # Don't bundle AppImage
  sed -i 's/"targets": "all",/"targets": "deb",/g' src-tauri/tauri.conf.json
}

build() {
  cd "$srcdir/$pkgname"
  export RUSTUP_TOOLCHAIN=stable
  yarn config set cache-folder "$srcdir/yarn-cache"
  yarn install
  yarn build
  yarn bundle
}

package() {
  cd "$srcdir/$pkgname"
  install -Dm755 "src-tauri/target/release/$pkgname" -t "$pkgdir/usr/bin/"
  install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir/usr/share/applications/"
}
