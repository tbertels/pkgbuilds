# https://aur.archlinux.org/packages/gnome-shell-extension-alphabetical-grid-extension-git
groups=('modified')

pkgname=gnome-shell-extension-alphabetical-grid-extension-git
pkgver=32.0.r0.g6b95792
pkgrel=1
pkgdesc="Restore the alphabetical ordering of the app grid, removed in GNOME 3.38 "
arch=('any')
url="https://github.com/stuarthayhurst/alphabetical-grid-extension"
license=('GPL3')
depends=('gnome-shell')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
_commit=6b957922b2b7730a44ef9171a8dedf317164cc24  # tags/v32.0 (last version to support gnome-shell <45)
source=("git+https://github.com/stuarthayhurst/alphabetical-grid-extension.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/alphabetical-grid-extension"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/alphabetical-grid-extension"
  make package
}

package() {
  _uuid='AlphabeticalAppGrid@stuarthayhurst'

  cd "$srcdir/alphabetical-grid-extension"
  install -d "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}"
  bsdtar xvf "build/$_uuid.shell-extension.zip" -C \
    "$pkgdir/usr/share/gnome-shell/extensions/$_uuid/"

  mv "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/locale" "$pkgdir/usr/share/"

  install -Dm644 "extension/schemas/org.gnome.shell.extensions.AlphabeticalAppGrid.gschema.xml" -t \
    "$pkgdir/usr/share/glib-2.0/schemas/"

  rm -rf "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/schemas/"
  rm "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/LICENSE.txt"
}
