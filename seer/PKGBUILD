# https://aur.archlinux.org/packages/seer-gdb

pkgname=seer
pkgver=1.8
pkgrel=1
pkgdesc="A GUI frontend to gdb"
arch=('x86_64' 'aarch64')
url="https://github.com/epasveer/seer"
license=('GPL3')
depends=('gdb' 'qt5-charts')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('9f7d76fb2026d53a2e1673b6e4fc3a2175881d3eaf9820d48ecdc9e2d10912a4')

build() {
  cmake -B build -S "$pkgname-$pkgver/src" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname-$pkgver"

  for icon_size in 32 64 128 256 512; do
    icons_dir="usr/share/icons/hicolor/${icon_size}x${icon_size}/apps"
    install -Dm644 "src/resources/${pkgname}_${icon_size}x${icon_size}.png" \
      "$pkgdir/${icons_dir}/$pkgname.png"
  done

  install -Dm644 "src/resources/$pkgname.desktop" -t "$pkgdir/usr/share/applications/"
}
