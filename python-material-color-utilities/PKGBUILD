# https://aur.archlinux.org/packages/python-material-color-utilities
groups=('modified')

pkgname=python-material-color-utilities
_name=material-color-utilities-python
pkgver=0.1.5
pkgrel=1
pkgdesc="Python port of material-color-utilities used for Material You colors"
arch=('any')
url="https://github.com/avanishsubbiah/material-color-utilities-python"
license=('Apache')
depends=('python' 'python-pillow' 'python-regex')
makedepends=('python-build' 'python-installer' 'python-poetry' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('3c6f02e7ce70595885447bdef37cf76fd3628c7c95fa2198d8174c269c951fae')

build() {
  cd "$_name-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$_name-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
