# https://www.archlinux.org/packages/community/any/glances
groups=('modified')

pkgname=glances
pkgver=3.2.4.2
pkgrel=1
pkgdesc="CLI curses-based monitoring tool"
arch=('any')
url="https://nicolargo.github.io/glances"
license=('LGPL')
depends=('python-defusedxml' 'python-psutil')
makedepends=('python-setuptools')
optdepends=('hddtemp: HDD temperature monitoring support'
#            'python-batinfo: '
#            'python-bernhard: for the Riemann export module'
            'python-bottle: for Web server mode'
            'python-cassandra-driver: for the Cassandra export module'
            'python-chevron: for the action script feature'
            'python-couchdb: for the CouchDB export module'
            'python-docker: for the Docker monitoring support'
            'python-elasticsearch: for the Elastic Search export module'
#            'python-graphitesender: for the Graphite export module'
            'python-influxdb: for the InfluxDB version 1 export module'
            'python-influxdb-client: for the InfluxDB version 2 export module'
            'python-kafka: for the Kafka export module'
            'python-netifaces: for the IP plugin'
            'python-py3nvml: for the GPU plugin'
            'python-paho-mqtt: '
            'python-pika: for the RabbitMQ/ActiveMQ export module'
#            'python-potsdb: for the OpenTSDB export module'
            'python-prometheus_client: for the Prometheus export module'
            'python-py-cpuinfo: for the Quicklook CPU info module'
            'python-pygal: for the graph export module'
#            'python-pymdstat: for RAID support'
            'python-pysnmp: for SNMP support'
#            'python-pysmart.smartx: for HDD Smart support'
            'python-pyzmq: for the ZeroMQ export module'
            'python-requests: for the Ports, Cloud plugins and RESTful export module'
            'python-sparklines: for the Quick Plugin sparklines option'
            'python-statsd: for the StatsD export module'
            'python-wifi: for the wifi plugin'
            'python-zeroconf: for the autodiscover mode'
            'wireless_tools: for the Wifi plugin')
changelog=NEWS.rst
source=("$pkgname-$pkgver.tar.gz::https://github.com/nicolargo/glances/archive/v$pkgver.tar.gz"
        "$pkgname.service")
sha256sums=('88ac8103d777b2a55cee9418a5940269c640c3ad964ec44de0e9bb80cdbead37'
            '4cdd6955ec14146a9975bd5a6fe0fe15081b0d83fc8a062e4a68b8e432317308')

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --root="$pkgdir/" --skip-build --optimize=1

  install -Dm644 "$srcdir/$pkgname.service" -t "$pkgdir/usr/lib/systemd/system"
}
