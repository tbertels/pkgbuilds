# https://gitlab.manjaro.org/packages/community/topgrade
# https://aur.archlinux.org/packages/topgrade
pkgname=topgrade
pkgver=6.8.0
pkgrel=1.1
pkgdesc='Invoke the upgrade procedure of multiple package managers'
arch=('x86_64' 'aarch64' 'armv7')
url='https://github.com/r-darwish/topgrade'
license=('GPL3')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('7ed24e5ca728482fc1c862a61e091cb5dfa5353f733c3458fbe1fda662e8fd41')

prepare() {
  cd "$pkgname-$pkgver"

  # Wrong flag for pip3 (fix #689)
  sed -i 's/Step::Pipx, "pip3",/Step::Pip3, "pip3",/g' src/main.rs
}

build() {
  cd "$pkgname-$pkgver"

  cargo build --release
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 target/release/$pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.8 "$pkgdir/usr/share/man/man8/$pkgname.8"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
