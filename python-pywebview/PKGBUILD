# https://aur.archlinux.org/packages/python-pywebview
groups=('modified')

pkgname=python-pywebview
_name=${pkgname#python-}
pkgver=4.4.1
pkgrel=1
pkgdesc="Build GUI for your Python program with JavaScript, HTML, and CSS."
arch=('any')
url="https://pywebview.flowrl.com"
license=('BSD')
depends=('python-bottle' 'python-cryptography' 'python-proxy_tools' 'python-typing_extensions')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
optdepends=('pyside2: use with QtPy, PySide2)'
            'pyside6: use with QtPy, PySide6)'
            'python-gobject: use with GTK'
            'python-pygobject-stubs: use with GTK'
            'python-pyqt5: use with Qt'
            'python-pyqt5-webengine: use with Qt'
            'python-qtpy: use with Qt'
            'webkit2gtk-4.1: use with GTK')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz"
        "https://raw.githubusercontent.com/r0x0r/pywebview/$pkgver/LICENSE")
sha256sums=('ea4c517e9265fadfd77937facb67787c07425d303b00b9d5c3a401bbb4576941'
            '4a988dd3598832cd3653de20dc33cb677d0fb53ab5551c879ca31280ae653675')

build() {
  cd "$_name-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$_name-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 "$srcdir/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
