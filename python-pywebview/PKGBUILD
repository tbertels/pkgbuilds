# https://aur.archlinux.org/packages/python-pywebview
groups=('modified')

pkgname=python-pywebview
_name=${pkgname#python-}
pkgver=5.0.3
pkgrel=1
pkgdesc="Build GUI for your Python program with JavaScript, HTML, and CSS."
arch=('any')
url="https://pywebview.flowrl.com"
license=('BSD-3-Clause')
depends=('python-bottle' 'python-cryptography' 'python-proxy_tools' 'python-typing_extensions')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
optdepends=('pyside2: use with QtPy, PySide2)'
            'pyside6: use with QtPy, PySide6)'
            'python-gobject: use with GTK'
            'python-pygobject-stubs: use with GTK'
            'python-pyqt5: use with Qt'
            'python-pyqt5-webengine: use with Qt'
            'python-qtpy: use with Qt'
            'webkit2gtk-4.1: use with GTK')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('b7fe887dc9cffd5cb7f5ed6bb56220007ad74d1e7f8b9dfd052897bbf346aec2')

build() {
  cd "$_name-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$_name-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
