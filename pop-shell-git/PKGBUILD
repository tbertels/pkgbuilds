# https://gitlab.manjaro.org/packages/community/gnome/gnome-shell-extension/-/tree/master/gnome-shell-extension-pop-shell
# https://aur.archlinux.org/packages/gnome-shell-extension-pop-shell-git
groups=('modified')

pkgname=('gnome-shell-extension-pop-shell-git' 'pop-shell-plugin-system76-power-git')
pkgbase='pop-shell-git'
pkgver=1.2.0.r114.g475b49d
pkgrel=2
pkgdesc="Pop!_OS Shell - Tiling window management in GNOME"
arch=('any')
url="https://github.com/pop-os/shell"
license=('GPL3')
makedepends=('git' 'typescript')
provides=("${pkgbase%-git}")
conflicts=("${pkgbase%-git}")
source=('git+https://github.com/pop-os/shell.git'
        'fix-resizing-window-with-mouse.patch')
sha256sums=('SKIP'
            '505fcc631a87ed444827f790b6233ccdd05dc78fd15a9c99935d1f1c5cd84c04')

pkgver() {
  cd "$srcdir/shell"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/shell"

  # Fix resizing/moving window with mouse in GNOME 40
  # https://github.com/laurentlbm/pop-shell/commit/e334b8d346db4d454034e53d18bdafcab7aacb17
  patch -Np1 -i $srcdir/fix-resizing-window-with-mouse.patch
}

build() {
  cd "$srcdir/shell"
  make compile
}

package_gnome-shell-extension-pop-shell-git() {
  depends=('fd' 'gnome-shell' 'pop-shell-shortcuts')
  optdepends=('gnome-control-center: keybinding categories'
              'gnome-shell-extensions: for Native Window Placment extension'
              'pop-shell-plugin-system76-power: for system76-power scripts')
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}")
  install='pop-shell.install'

  cd "$srcdir/shell"
  make DESTDIR="$pkgdir" install

  install -Dm644 schemas/org.gnome.shell.extensions.pop-shell.gschema.xml -t \
    "$pkgdir/usr/share/glib-2.0/schemas"
  install -Dm644 keybindings/*.xml -t \
    "$pkgdir/usr/share/gnome-control-center/keybindings"
  install -Dm755 scripts/configure.sh -t \
    "$pkgdir/usr/share/gnome-shell/extensions/pop-shell@system76.com/scripts"
}

package_pop-shell-plugin-system76-power-git() {
  pkgdesc="Launcher integration with system76-power"
  depends=('gnome-shell-extension-pop-shell' 'gnome-terminal' 'system76-power')
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}")

  cd "$srcdir/shell"
  make DESTDIR="$pkgdir" install-system76-plugins
}
