# https://gitlab.manjaro.org/packages/community/gnome/gnome-shell-extension/-/tree/master/gnome-shell-extension-pop-shell
# https://aur.archlinux.org/packages/gnome-shell-extension-pop-shell-git
groups=('modified')

pkgname=('gnome-shell-extension-pop-shell-git')
pkgbase='pop-shell-git'
pkgver=1.2.0.r128.g0484ffb
pkgrel=1
pkgdesc="Pop!_OS Shell - Tiling window management in GNOME"
arch=('any')
url="https://github.com/pop-os/shell"
license=('GPL3')
depends=('gnome-shell' 'pop-launcher' 'pop-shell-shortcuts')
makedepends=('git' 'typescript')
optdepends=('gnome-control-center: keybinding categories'
            'gnome-shell-extensions: for Native Window Placement extension')
source=('git+https://github.com/pop-os/shell.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/shell"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/shell"
  make
}

package_gnome-shell-extension-pop-shell-git() {
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}")
  install="${pkgbase%-git}.install"

  cd "$srcdir/shell"
  make DESTDIR="$pkgdir" install

  install -Dm644 schemas/org.gnome.shell.extensions.pop-shell.gschema.xml -t \
    "$pkgdir/usr/share/glib-2.0/schemas"
  install -Dm644 keybindings/*.xml -t \
    "$pkgdir/usr/share/gnome-control-center/keybindings"
  install -Dm755 scripts/configure.sh -t \
    "$pkgdir/usr/share/gnome-shell/extensions/pop-shell@system76.com/scripts"
}
