pkgname=elementary-frog
pkgver=0.3.0
pkgrel=1
pkgdesc="Extract text from any image, video or anything."
arch=('x86_64')
url="https://tenderowl.com/work/frog"
license=('MIT')
depends=('granite' 'gtk3' 'leptonica' 'libhandy' 'libnotify' 'libportal'
         'python-gobject' 'python-pydbus' 'python-pytesseract' 'pyzbar')
makedepends=('git' 'meson')
#checkdepends=('appstream-glib')
#source=("$pkgname-$pkgver.tar.gz::https://github.com/TenderOwl/Frog/archive/refs/tags/$pkgver.tar.gz")
_commit=af56d57b6220bd688fcd84c825709ae2d7b931b7
source=("git+https://github.com/TenderOwl/Frog.git#commit=$_commit"
        'com.github.tenderowl.frog.desktop.in')
sha256sums=('SKIP'
            '58c82ebc8ffe5f7f65b446ca7578c0ce98778737bf4098de83157ce8ac9d3019')

pkgver() {
  cd "$srcdir/Frog"
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd "$srcdir/Frog"

  # Workaround for undefined XDG_DATA_HOME env var
  cp -f ../com.github.tenderowl.frog.desktop.in data/

  # Fix path to appdata
  sed -i 's|/app/share/|/usr/share/|g' frog/language_manager.py
}

build() {
  arch-meson Frog build
  meson compile -C build
}

check() {

  # Validate appstream file test fails
  meson test 'Validate desktop file' 'Validate schema file' -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  cd "$srcdir/Frog"
  install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/"
}
