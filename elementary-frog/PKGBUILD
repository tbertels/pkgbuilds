pkgname=elementary-frog
pkgver=0.1.7
pkgrel=1
pkgdesc="Extract text from any image, video or anything."
arch=('x86_64')
url="https://tenderowl.com/work/frog"
license=('MIT')
depends=('granite' 'gtk3' 'leptonica' 'libhandy' 'libnotify' 'python-pydbus'
         'python-pytesseract')
makedepends=('meson')
checkdepends=('appstream-glib')
source=("$pkgname-$pkgver.tar.gz::https://github.com/TenderOwl/Frog/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('53a354a3b3fa85f8b545951e1d0b1c6abdaea3a8be186e1b5f75557fd9f9d15f')

prepare() {
  cd "Frog-$pkgver"

  # correct version
  sed -i "s/version: '0.1.0'/version: '$pkgver'/g" meson.build
}

build() {
  arch-meson "Frog-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  cd "Frog-$pkgver"
  install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/"
}
