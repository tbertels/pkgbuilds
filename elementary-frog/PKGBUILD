pkgname=elementary-frog
pkgver=0.2.1+8+g5e87cd6
pkgrel=1
pkgdesc="Extract text from any image, video or anything."
arch=('x86_64')
url="https://tenderowl.com/work/frog"
license=('MIT')
depends=('granite' 'gtk3' 'leptonica' 'libhandy' 'libnotify' 'libportal'
         'python-gobject' 'python-pydbus' 'python-pytesseract' 'pyzbar')
makedepends=('git' 'meson' 'setconf')
checkdepends=('appstream-glib')
#source=("$pkgname-$pkgver.tar.gz::https://github.com/TenderOwl/Frog/archive/refs/tags/$pkgver.tar.gz")
_commit=5e87cd68e2caca72708cd7d64af0acd47da599b9
source=("git+https://github.com/TenderOwl/Frog.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/Frog"
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd "$srcdir/Frog"

  # Workaround for undefined XDG_DATA_HOME env var
  setconf data/com.github.tenderowl.frog.desktop.in Exec 'env XDG_DATA_HOME="\\$HOME/.local/share/" frog'

  # Fix path to appdata
  sed -i 's|/app/share/|/usr/share/|g' frog/language_manager.py
}

build() {
  arch-meson Frog build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  cd "$srcdir/Frog"
  install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/"
}
