# http://aur.archlinux.org/packages/ternimal-git
groups=('modified')

pkgname=ternimal-git
pkgver=0.1.0.r2.ge7953b4
pkgrel=1
pkgdesc="Simulate a lifeform in the terminal"
arch=('x86_64' 'i686' 'armv7h')
url="https://github.com/p-e-w/ternimal"
license=('GPL3')
depends=('gcc-libs')
makedepends=('rustc' 'git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/p-e-w/ternimal.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/${pkgname%-git}"
  export RUSTUP_TOOLCHAIN=stable
  rustc -O "${pkgname%-git}.rs"
}

package() {
  cd "$srcdir/${pkgname%-git}"
  install -Dm755 "${pkgname%-git}" -t "$pkgdir/usr/bin"
}
