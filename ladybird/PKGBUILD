# https://aur.archlinux.org/packages/ladybird
groups=('modified')

# Contributor: Francesco Gazzetta <https://github.com/fgaz>
# https://github.com/fgaz/nixpkgs/blob/ladybird/init/pkgs/applications/networking/browsers/ladybird/default.nix

pkgname=ladybird
pkgver=r64.7040a87
pkgrel=1
pkgdesc="A browser using the SerenityOS LibWeb engine with a Qt GUI."
arch=('x86_64')
url="https://github.com/awesomekling/ladybird"
license=('BSD')
depends=('libgl' 'qt6-base' 'qt6-wayland')
makedepends=('cmake' 'git' 'ninja' 'patchelf' 'qt6-tools' 'unzip')
optdepends=()
#options=('!strip') # Stripping results in a symbol lookup error
_commit=7040a8790c1a8cb4879abe04b1198ec6e64d1c92
source=("git+https://github.com/awesomekling/ladybird.git#commit=$_commit"
        "$pkgname.sh")
sha256sums=('SKIP'
            '1d0fc030877f9074443ef71835518c043908360b5dac67d907d8b2317baa37c4')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {

  # prevent static lib mangling with LTO
  CFLAGS+=" -ffat-lto-objects"
  CXXFLAGS+=" -ffat-lto-objects"

  cmake -GNinja -B build -S "$pkgname" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DENABLE_TIME_ZONE_DATABASE_DOWNLOAD='false' \
    -DENABLE_UNICODE_DATABASE_DOWNLOAD='false' \
    -DBUILD_SHARED_LIBS='OFF' \
    -Wno-dev
  cmake --build build
  ninja -C build
}

package() {

  # Upstream install rules are missing
  # https://github.com/awesomekling/ladybird/issues/36
#  DESTDIR="$pkgdir" ninja install -C build

  cd "$srcdir/$pkgname"
  install -Dm644 build/_deps/lagom-build/*.so* -t "$pkgdir/usr/lib/$pkgname/"

  # According to the readme, the program needs access to the serenity sources
  # at runtime
  cp -R build/serenity "$pkgdir/usr/share/"
  install -Dm755 "build/$pkgname" -t "$pkgdir/usr/share/serenity/Base/bin/"
  install -Dm755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"

  # Patch rpaths
  # https://github.com/awesomekling/ladybird/issues/36
  for f in "$pkgdir/usr/share/serenity/Base/bin/$pkgname" "$pkgdir/usr/lib/$pkgname"/*.so; do
      old_rpath=$(patchelf --print-rpath "${f}")
      # Remove reference to libraries from build directory
      rpath_without_build=$(sed -e 's@[^:]*/_deps/lagom-build:@@g' <<< ${old_rpath})
      # Add directory where we install those libraries
      new_rpath="$pkgdir/usr/lib/$pkgname":${rpath_without_build}
      patchelf --set-rpath "${new_rpath}" "${f}"
    done

  install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
