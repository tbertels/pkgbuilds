pkgname=g-assist-git
pkgver=1.0.0.rc.2.r152.g6264bc0
pkgrel=2
pkgdesc="A cross-platform unofficial Google Assistant Client for Desktop (powered by Google Assistant SDK)"
arch=('x86_64')
url="https://github.com/Melvin-Abraham/Google-Assistant-Unofficial-Desktop-Client"
license=('Apache')
depends=('electron10')
makedepends=('git' 'nvm')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+https://github.com/Melvin-Abraham/Google-Assistant-Unofficial-Desktop-Client.git"
        "${pkgname%-git}.desktop"
        "${pkgname%-git}.sh"
        )
sha256sums=('SKIP'
            'd56962ceefe6a87d9a161655549adb56cff6961f142ecb141654cddb7e314cb5'
            '5e462519de1e1301f224a9fd81af3e9387085cb60e0e5bc848a6d550c2eff9f2')

_ensure_local_nvm() {
  # let's be sure we are starting clean
  which nvm >/dev/null 2>&1 && nvm deactivate && nvm unload
  export NVM_DIR="$srcdir/.nvm"

  # The init script returns 3 if version specified
  # in ./.nvrc is not (yet) installed in $NVM_DIR
  # but nvm itself still gets loaded ok
  source /usr/share/nvm/init-nvm.sh || [[ $? != 1 ]]
}

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/${pkgname%-git}"
  export npm_config_cache="$srcdir/npm_cache"
  _ensure_local_nvm
  nvm install 14.18.0
}

build() {
  cd "$srcdir/${pkgname%-git}"
  export npm_config_cache="$srcdir/npm_cache"
  _ensure_local_nvm
  electronDist=/usr/lib/electron10
  electronVer=$(sed s/^v// /usr/lib/electron10/version)
  npm install
  ./node_modules/.bin/electron-builder --linux --x64 --dir \
    $dist -c.electronDist=$electronDist -c.electronVersion=$electronVer
}

package() {
  cd "$srcdir/${pkgname%-git}"
  install -d "$pkgdir/usr/lib/${pkgname%-git}"
  cp -r dist/linux-unpacked/resources "$pkgdir/usr/lib/${pkgname%-git}"

  install -Dm644 "app/res/Assistant Logo.png" "$pkgdir/usr/share/pixmaps/${pkgname%-git}.png"
  install -Dm644 "app/res/Assistant Logo.svg" \
    "$pkgdir/usr/share/icons/hicolor/scalable/apps/${pkgname%-git}.svg"
  install -Dm644 "$srcdir/${pkgname%-git}.desktop" -t "$pkgdir/usr/share/applications"
  install -Dm755 "$srcdir/${pkgname%-git}.sh" "$pkgdir/usr/bin/${pkgname%-git}"
}
