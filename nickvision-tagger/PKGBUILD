# https://aur.archlinux.org/packages/nickvision-tagger
groups=('modified')

pkgname=nickvision-tagger
pkgver=2022.9.2
pkgrel=1
pkgdesc="An easy-to-use music tag (metadata) editor"
arch=('x86_64')
url="https://github.com/nlogozzo/NickvisionTagger"
license=('GPL3')
depends=('chromaprint' 'jsoncpp' 'libadwaita' 'libcurlpp' 'taglib')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('6b23f6cb2bd26dd6a8e83e6b84ef3d62295de75f662895b3eb97eec7c24cefe9')

build() {
  arch-meson NickvisionTagger-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  # conflicts with system chromaprint
  rm "$pkgdir/usr/bin/fpcalc"

  ln -s /usr/bin/org.nickvision.tagger "$pkgdir/usr/bin/$pkgname"
}
