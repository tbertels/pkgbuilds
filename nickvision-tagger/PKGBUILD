# https://aur.archlinux.org/packages/nickvision-tagger
groups=('modified')

pkgname=nickvision-tagger
pkgver=2022.10.1
pkgrel=1
pkgdesc="An easy-to-use music tag (metadata) editor"
arch=('x86_64')
url="https://github.com/nlogozzo/NickvisionTagger"
license=('GPL3')
depends=('chromaprint' 'jsoncpp' 'libadwaita' 'libcurlpp' 'taglib')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('9bae09a1288ca803420ddc7bf36c6d5667be39dc22e4dc47f29ce0da4876b31a')

build() {
  arch-meson NickvisionTagger-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  # conflicts with system chromaprint
  rm "$pkgdir/usr/bin/fpcalc"

  ln -s /usr/bin/org.nickvision.tagger "$pkgdir/usr/bin/$pkgname"
}
