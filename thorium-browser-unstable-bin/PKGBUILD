pkgname=thorium-browser-unstable-bin
pkgver=105.0.5183.0
pkgrel=1
pkgdesc="Chromium fork for Linux named after radioactive element No. 90."
arch=('x86_64')
url="https://thorium.rocks"
license=('GPL3')
depends=('alsa-lib' 'ca-certificates' 'dbus' 'gtk3' 'libcups' 'nss' 'ttf-liberation' 'xdg-utils')
optdepends=('pipewire: WebRTC desktop sharing under Wayland'
            'kdialog: support for native dialogs in Plasma'
            'org.freedesktop.secrets: password storage backend on GNOME / Xfce'
            'kwallet: support for storing passwords in KWallet on Plasma')
provides=("${pkgname%-bin}")
conflicts=("${pkgname%-bin}" 'chromium')
options=('!emptydirs' '!strip')
source=("https://github.com/Alex313031/Thorium/releases/download/M${pkgver}/thorium-browser_${pkgver}_amd64.deb"
        "${pkgname%-bin}.sh")
sha256sums=('d293fcb94eec6a93dc6134ab8b524bed399e5841cf2e94b1ef06f8d522d4b30f'
            '7464a2b7cff28ba7e6e5118cceeb8ee260f9a11b6ad983ca5e828dc00e38fd5e')

package() {
  bsdtar -xf data.tar.xz -C "$pkgdir/"

  install -Dm755 "${pkgname%-bin}.sh" \
    "$pkgdir/opt/chromium.org/thorium/${pkgname%-bin}"

  chmod 4755 "$pkgdir/opt/chromium.org/thorium/chrome-sandbox"

  for icon_size in 16 24 32 48 64 128 256; do
    icons_dir=usr/share/icons/hicolor/${icon_size}x${icon_size}/apps
    install -Dm644 "$pkgdir/opt/chromium.org/thorium/product_logo_${icon_size}.png" \
      "$pkgdir/${icons_dir}/${pkgname%-bin}.png"
  done

  # Fix Thorium Shell icon path
  sed -i 's/thorium-unstable/thorium/g' "$pkgdir/usr/share/applications/thorium-shell.desktop"

  rm -r \
    "$pkgdir"/etc/ \
    "$pkgdir"/opt/chromium.org/thorium/cron/ \
    "$pkgdir"/opt/chromium.org/thorium/product_logo_*.{png,xpm} \
    "$pkgdir"/usr/share/menu/
}
