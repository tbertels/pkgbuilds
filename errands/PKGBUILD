# https://aur.archlinux.org/packages/errands
groups=('modified')

pkgname=errands
pkgver=45.1.2
pkgrel=1
pkgdesc="Todo application for those who prefer simplicity."
arch=('any')
url="https://github.com/mrvladus/Errands"
license=('MIT')
depends=('hicolor-icon-theme' 'libadwaita' 'libsecret' 'python-caldav'
         'python-gobject' 'python-icalendar')
makedepends=('meson')
checkdepends=('appstream-glib')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('ace0bbd9315510a9a88f830364627ad04d99040fad7398ec6e90d0c949fc7644')

build() {
  arch-meson "Errands-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"

  cd "Errands-$pkgver"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"

  # Fix permissions
  chmod 0755 "$pkgdir/usr/bin/$pkgname"
}
