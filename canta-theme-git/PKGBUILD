# https://aur.archlinux.org/packages/canta-gtk-theme-git
pkgname=('canta-gtk-theme-git' 'canta-icon-theme-git')
pkgbase=canta-theme-git
pkgdesc="Flat Material Design theme for GTK 3, GTK 2 and Gnome-Shell"
pkgver=2020.05.17.r5.45cc14e
pkgrel=3
arch=('any')
url="https://www.gnome-look.org/p/1220749"
license=('GPL2')
makedepends=('gtk3' 'git' 'sassc')
optdepends=('gtk-engine-murrine: GTK2 theme support'
            'gtk-engines: GTK2 theme support'
provides=("${pkgbase%-git}")
conflicts=("${pkgbase%-git}")
options=(!strip)
source=("${pkgbase%-git}::git+https://github.com/vinceliuice/Canta-theme.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgbase%-git}"
	git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g'
}

package_canta-gtk-theme-git() {
	depends=('gnome-themes-extra')
	optdepends=('gtk-engine-murrine: GTK2 theme support'
	            'gtk-engines: GTK2 theme support')
	provides=("${pkgname%-git}")
	conflicts=("${pkgname%-git}")

	cd "$srcdir/${pkgbase%-git}"
	install -d "$pkgdir/usr/share/themes"

	# Install theme with Nautilus background image
	./install.sh -d "$pkgdir/usr/share/themes"

	# Install theme without Nautilus background image
	#./install.sh -o -d "$pkgdir/usr/share/themes"
}

package_canta-icon-theme-git() {
	pkgdesc="Flat icons for Canta GTK theme"
	optdepends=('numix-circle-icon-theme-git: if you want a better experience')
	provides=("${pkgname%-git}")
	conflicts=("${pkgname%-git}")

	cd "$srcdir/${pkgbase%-git}"
	install -d "$pkgdir/usr/share/icons"
	cp -r icons/Canta "$pkgdir/usr/share/icons"
}
