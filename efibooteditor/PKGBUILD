# https://aur.archlinux.org/packages/efibooteditor
groups=('modified')

pkgname=efibooteditor
pkgver=1.4.0
pkgrel=1
pkgdesc="GUI for editing EFI Boot Manager entries"
arch=('x86_64')
url="https://github.com/Neverous/efibooteditor"
license=('LGPL3')
depends=('efivar' 'qt6-base')
makedepends=('cmake' 'qt6-tools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('676ae9c9169f4a3b7d6011e7bb089f7ad215253fe480edadc3b8dfd19d53806e')

build() {
  export BUILD_VERSION="$pkgver"
  cmake -B build -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DQT_VERSION_MAJOR='6' \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
