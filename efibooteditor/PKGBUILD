# https://aur.archlinux.org/packages/efibooteditor
groups=('modified')

pkgname=efibooteditor
pkgver=1.2.2
pkgrel=1
pkgdesc="GUI for editing EFI Boot Manager entries"
arch=('x86_64')
url="https://github.com/Neverous/efibooteditor"
license=('LGPL3')
depends=('efivar' 'qt6-5compat')
makedepends=('cmake' 'qt6-tools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('d58aa3db8ffa9cc32e8c327f1118b1c2249b635a9332460642994d20f344f2e9')

build() {
  export BUILD_VERSION="$pkgver"
  cmake -B build -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DQT_VERSION_MAJOR='6' \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
