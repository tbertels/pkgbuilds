pkgname=findex
pkgver=0.8.0
pkgrel=1
pkgdesc="Highly customizable finder with high performance."
arch=('x86_64')
url="https://github.com/mdgaziur/findex"
license=('GPL3')
depends=('gtk3' 'libkeybinder3')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('dd18c7c7175174e4f98aeffd7f7a50f0816d4d0cef46babd1dfb47a380a63aed')

prepare() {
  cd "$pkgname-$pkgver"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --all-features
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 target/release/{"$pkgname","$pkgname-daemon"} -t "$pkgdir/usr/bin/"
  install -Dm644 css/style.css -t "$pkgdir/opt/$pkgname/"
}
