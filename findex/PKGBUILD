pkgname=findex
pkgver=0.7.1
pkgrel=1
pkgdesc="Highly customizable finder with high performance."
arch=('x86_64')
url="https://github.com/mdgaziur/findex"
license=('GPL3')
depends=('libkeybinder3')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('057c9b1f7826ff502efae0b587069ed75ffb61c880f7f58c0705f6b1473ef4fe')

prepare() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --all-features
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 target/release/{"$pkgname","$pkgname-daemon"} -t "$pkgdir/usr/bin/"
  install -Dm644 css/style.css -t "$pkgdir/opt/$pkgname/"
}
