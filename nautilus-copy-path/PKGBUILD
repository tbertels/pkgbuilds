# https://aur.archlinux.org/packages/nautilus-copy-path
groups=('modified')

pkgname=nautilus-copy-path
pkgver=1.9.0
pkgrel=1
pkgdesc="Extension for Nautilus to copy files/dir path or name"
arch=('any')
url="https://github.com/chr314/nautilus-copy-path"
license=('MIT')
depends=('python-nautilus' 'python-gobject')
makedepends=('git')
backup=("usr/share/nautilus-python/extensions/$pkgname/config.json")
_commit=4411075aa16741f9459ef0e4c834753ef4e89325  # tags/1.9.0^0
source=("git+https://github.com/chr314/nautilus-copy-path.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --tags | sed 's/-/+/g'
}

package() {
  cd "$srcdir/$pkgname"
  install -Dm644 "$pkgname.py" -t "$pkgdir/usr/share/nautilus-python/extensions"
  install -Dm644 nautilus_copy_path.py translation.py config.json -t \
    "$pkgdir/usr/share/nautilus-python/extensions/$pkgname"
  install -Dm644 translations/*.json -t \
    "$pkgdir/usr/share/nautilus-python/extensions/$pkgname/translations"

  # Compile Python bytecode:
  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
