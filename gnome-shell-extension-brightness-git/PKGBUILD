pkgname=gnome-shell-extension-brightness-git
_pkgname=gnome-extension-brightness
pkgver=r48.5e920a5
pkgrel=1
pkgdesc="Simple GNOME extension to control displays' brightness via DDC."
arch=('any')
url="https://gitlab.com/bruno.englert/gnome-extension-brightness"
license=('GPL')
depends=('ddcutil' 'gnome-shell')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://gitlab.com/bruno.englert/gnome-extension-brightness.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/${_pkgname}"
  gnome-extensions pack \
    --extra-source=services/ \
    --extra-source=ui/ \
    --extra-source=icon.png \
    --force
}

package() {
  cd "$srcdir/${_pkgname}"
  _uuid="${_pkgname}@bruno.englert.gitlab.com"

  install -d "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}"
  bsdtar xvf "${_pkgname}@bruno.englert.gitlab.com.shell-extension.zip" -C \
    "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/"
}
