# https://aur.archlinux.org/packages/gnome-firmware-git
groups=('modified')

pkgname=gnome-firmware-git
_app_id=org.gnome.Firmware
pkgver=3.36.0.r25.gc9314a6
pkgrel=1
pkgdesc="Manage firmware on devices supported by fwupd"
arch=('x86_64')
url="https://gitlab.gnome.org/hughsie/gnome-firmware-updater"
license=('GPL2')
depends=('gtk3' 'fwupd' 'libxmlb' 'libsoup' 'systemd')
makedepends=('git' 'meson' 'help2man')
checkdepends=('appstream-glib')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+https://gitlab.gnome.org/hughsie/gnome-firmware-updater.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  arch-meson "${pkgname%-git}" build
  meson compile -C build
}

check() {

# No tests defined
#  meson test -C build --print-errorlogs

  desktop-file-validate build/data/${_app_id}.desktop
  appstream-util validate-relax --nonet build/data/appdata/${_app_id}.metainfo.xml
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

