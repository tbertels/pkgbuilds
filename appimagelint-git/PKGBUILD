# WIP

pkgname=appimagelint-git
pkgver=r109.956ab95
pkgrel=1
pkgdesc="Check AppImages for compatibility, best practices etc."
arch=('any')
url="https://github.com/TheAssassin/appimagelint"
license=('MIT')
depends=('python-coloredlogs' 'python-requests' 'python-setuptools' 'python-pyxdg'
         'python-pillow')
makedepends=('git' 'python-build' 'python-installer' 'python-wheel')
optdepends=()
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/TheAssassin/appimagelint.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/${pkgname%-git}"
  python -m build --wheel --no-isolation
}

package() {
  cd "$srcdir/${pkgname%-git}"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname"
}
