# https://aur.archlinux.org/packages/envycontrol
groups=('modified')

pkgname=envycontrol
pkgver=3.3.0
pkgrel=1
pkgdesc="Easy GPU switching for Nvidia Optimus laptops"
arch=('any')
url="https://bayas.dev/projects/envycontrol"
license=('MIT')
depends=('python' 'xorg-xrandr')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
optdepends=('gnome-shell-extension-gpu-profile-selector: GUI for GNOME')
conflicts=('optimus-manager')
source=("$pkgname-$pkgver.tar.gz::https://github.com/bayasdev/envycontrol/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('6f045c4ada025ec4fb27777cb43314dbfd483115201eee04899fed9643b63408')

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
