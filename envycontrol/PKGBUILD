# https://aur.archlinux.org/packages/envycontrol
groups=('modified')

pkgname=envycontrol
pkgver=3.0.2
pkgrel=1
pkgdesc="Easy GPU switching for Nvidia Optimus laptops under Linux"
arch=('any')
url="https://bayas.dev/projects/envycontrol"
license=('MIT')
depends=('python' 'xorg-xrandr')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
optdepends=('gnome-shell-extension-gpu-profile-selector: GUI for GNOME')
source=("$pkgname-$pkgver.tar.gz::https://github.com/bayasdev/envycontrol/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('e6bbf491d5caef8dfdf0e299735c78d6994526d0c12d203c2faef0e9a131716b')

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
