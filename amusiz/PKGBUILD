# https://aur.archlinux.org/packages/amusiz
groups=('modified')

# Currently broken:
# https://github.com/mirkobrombin/Amusiz/issues/8

pkgname=amusiz
pkgver=1.3.4
pkgrel=1
pkgdesc="A GTK Amazon Music client for Linux"
arch=('any')
url="https://github.com/mirkobrombin/Amusiz"
license=('GPL3')
depends=('dconf' 'gst-libav' 'gst-plugins-good' 'gtk3' 'hicolor-icon-theme'
         'libhandy' 'libnotify' 'python-gobject' 'webkit2gtk')
makedepends=('meson' 'ninja')
checkdepends=('appstream-glib')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('c59773bdffea1dd5af4493561bd74e0ed47bf8c199cc69a34a20606a1fb1b27d')

prepare() {
  cd "Amusiz-$pkgver"

  # Correct version
  sed -i "s/1.3.3/1.3.4/g" src/about.ui
  sed -i 's/version="1.3.3" date="2021-07-09"/version="1.3.4" date="2021-07-11"/g' \
    data/pm.mirko.Amusiz.appdata.xml.in
}

build() {
  arch-meson "Amusiz-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
