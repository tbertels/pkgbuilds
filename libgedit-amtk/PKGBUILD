pkgname=libgedit-amtk
pkgver=5.8.0
pkgrel=1
pkgdesc="Actions, Menus and Toolbars Kit for GTK applications"
arch=('x86_64')
url="https://gedit-technology.net"
license=('LGPL3')
depends=('glib2' 'glibc' 'gtk3')
makedepends=('gobject-introspection' 'gtk-doc' 'meson')
provides=('libgedit-amtk-5.so=0')
conflicts=('amtk')
replaces=('amtk')
source=("https://gedit-technology.net/tarballs/libgedit-amtk/$pkgname-$pkgver.tar.xz")
sha256sums=('64017ae100ef588e01ef54d79c13c4b9767fd37e4365d7e4afd924f751460ecc')

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
