groups=('modified')

pkgname=endeavour
pkgver=42.0
pkgrel=1
pkgdesc="Task manager for GNOME"
arch=('x86_64' 'aarch64')
url="https://gitlab.gnome.org/World/Endeavour"
license=('GPL3')
#groups=('gnome-extra')
depends=('evolution-data-server' 'libpeas' 'librest' 'python' 'libportal-gtk4'
         'libadwaita>=1.2')
makedepends=('git' 'gobject-introspection' 'meson' 'yelp-tools')
checkdepends=('appstream-glib')
provides=('gnome-todo')
conflicts=('gnome-todo')
replaces=('gnome-todo')
_commit=188ef00f6194c1dd36d39279c2d7a5429a68d3f5
source=("git+https://gitlab.gnome.org/World/Endeavour.git#commit=${_commit}"
        '132.patch')
sha256sums=('SKIP'
            'de59ba119557a78b113dbb8f1ac5005d3069bc5f52bb407a75099a0e27cfb809')

pkgver() {
  cd "$srcdir/Endeavour"
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd "$srcdir/Endeavour"
  patch -Np1 -i "$srcdir/132.patch"
}

build() {
  arch-meson Endeavour build
  meson compile -C build
}

check() (
  glib-compile-schemas "${GSETTINGS_SCHEMA_DIR:=$PWD/Endeavour/data}"
  export GSETTINGS_SCHEMA_DIR

  meson test -C build || :
)

package() {
  meson install -C build --destdir "$pkgdir"
}
