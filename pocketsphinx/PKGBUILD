# https://aur.archlinux.org/packages/pocketsphinx
groups=('modified')

pkgname=pocketsphinx
pkgver=5prealpha
pkgrel=11
pkgdesc='Lightweight speech recognition engine, specifically tuned for handheld and mobile devices, though it works equally well on the desktop.'
arch=('x86_64')
url="https://cmusphinx.github.io"
license=('BSD')
depends=("sphinxbase=$pkgver" 'gst-plugins-base-libs')
makedepends=('swig')
provides=('_pocketsphinx.so=0' 'libpocketsphinx.so=3' 'libgstpocketsphinx.so')
source=("https://downloads.sourceforge.net/cmusphinx/$pkgname-$pkgver.tar.gz")
sha256sums=('ef5bb5547e2712bdf571f256490ef42a47962033892efd9d7df8eed7fe573ed9')

prepare() {
  cd "$pkgname-$pkgver"

  # Reconfigure project for current version of Automake
  autoreconf -ivf > /dev/null
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
