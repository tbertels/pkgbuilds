# https://aur.archlinux.org/packages/git-extras
groups=('modified')

pkgname=git-extras
pkgver=6.3.0
pkgrel=1
pkgdesc="GIT utilities -- repo summary, commit counting, repl, changelog population and more"
arch=('any')
url="https://github.com/tj/git-extras"
license=('MIT')
depends=('git')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('8a218a0c8e10036d5ba14f26b70f994b0d11166b02ef3fed71c593cef026ec3d')
b2sums=('263e192015e46da7867f131c19ae47243e60355cba02b1860b45abafdc4fc6adf916dffce58b853960d8bb22f5da067e1dca6ed0b02f108953571896d1c981c4')

package() {
  cd "$pkgname-$pkgver"

  # avoid annoying interactive prompts if an alias is in your gitconfig
  export GIT_CONFIG=/dev/null

  make DESTDIR="$pkgdir" PREFIX=/usr SYSCONFDIR=/etc install

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 etc/"$pkgname"{.fish,-completion.zsh} -t "$pkgdir/usr/share/doc/$pkgname/completions"
}
