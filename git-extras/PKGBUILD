# https://aur.archlinux.org/packages/git-extras
groups=('modified')

pkgname=git-extras
pkgver=6.4.0
pkgrel=1
pkgdesc="GIT utilities -- repo summary, commit counting, repl, changelog population and more"
arch=('any')
url="https://github.com/tj/git-extras"
license=('MIT')
depends=('git')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('d8943c0caab43e70c23890816a9775844d33261c40d5be03c1e012c276b1aa63')
b2sums=('bffbc8efcf1cf030bceb20b953e1cbbc067c8383ced38b786c21eb9bd9b7d3febd3d8aae652c0dd731de688d3cfa225c3aec91d6b9c590459b576db113ca0af0')

package() {
  cd "$pkgname-$pkgver"

  # avoid annoying interactive prompts if an alias is in your gitconfig
  export GIT_CONFIG=/dev/null

  make DESTDIR="$pkgdir" PREFIX=/usr SYSCONFDIR=/etc install

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 etc/"$pkgname"{.fish,-completion.zsh} -t "$pkgdir/usr/share/doc/$pkgname/completions"
}
