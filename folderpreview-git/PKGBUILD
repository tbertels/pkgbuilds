# https://aur.archlinux.org/packages/folderpreview-git
groups=('modified')

pkgname=folderpreview-git
pkgver=0.4.3.r0.gdcd1838
pkgrel=1
pkgdesc="Generates folder thumbs using child files thumbs"
arch=('any')
url="https://gitlab.com/hxss-linux/folderpreview"
license=('MIT')
depends=('python-colorlog' 'python-dbus-next' 'python-gobject' 'python-parse'
         'python-pyvips' 'python-pyxdg' 'python-systemd' 'python-yaml')
makedepends=('git' 'python-setuptools')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://gitlab.com/hxss-linux/folderpreview.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/${pkgname%-git}"
  python setup.py build
}

package() {
  cd "$srcdir/${pkgname%-git}"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 resources/template.thumbnailer -t "$pkgdir/usr/share/thumbnailers"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
