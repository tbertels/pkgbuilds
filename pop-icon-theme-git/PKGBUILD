# https://aur.archlinux.org/packages/pop-icon-theme-git
groups=('modified')

pkgname=pop-icon-theme-git
pkgver=2.1.0.r2415.a5a10f9725
pkgrel=1
pkgdesc="Pop Icons"
arch=('any')
url="https://github.com/pop-os/icon-theme"
license=('CC-BY-SA-4.0')
depends=('adwaita-icon-theme')
makedepends=('git' 'dpkg' 'meson')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=('!strip')
source=('git+https://github.com/pop-os/icon-theme.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/icon-theme"
  printf "%s.r%s.%s" "$(dpkg-parsechangelog --show-field Version | sed 's/1://;s/-/./')" \
    "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  arch-meson icon-theme build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  cd "$srcdir/icon-theme"
  install -Dm644 COPYING LICENSE -t "$pkgdir/usr/share/licenses/${pkgname%-git}"
}
