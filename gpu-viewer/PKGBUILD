# https://aur.archlinux.org/packages/gpu-viewer
groups=('modified')

pkgname=gpu-viewer
pkgver=2.29
pkgrel=1
pkgdesc="A front-end to glxinfo, vulkaninfo, clinfo and es2_info"
arch=('any')
url="https://github.com/arunsivaramanneo/GPU-Viewer/"
license=('GPL3')
depends=('clinfo' 'gtk4' 'lsb-release' 'mesa-utils' 'python'
         'python-gobject' 'vdpauinfo' 'vulkan-tools')
makedepends=('meson')
checkdepends=('appstream-glib')
optdepends=('nvidia: Vulkan NVIDIA drivers'
            'vulkan-radeon: Vulkan AMD drivers'
            'vulkan-intel: Vulkan Intel drivers')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('e2035829606652c15a361cc01a2fb7fe4ba363b653a152120a7f3dc9d77edb26')

build() {
  arch-meson GPU-Viewer-$pkgver build
  meson compile -C build
}

check() {
  cd GPU-Viewer-$pkgver
  appstream-util validate-relax --nonet data/io.github.arunsivaramanneo.GPUViewer.metainfo.xml
  desktop-file-validate data/io.github.arunsivaramanneo.GPUViewer.desktop
}

package() {
  meson install -C build --destdir "$pkgdir"
}
