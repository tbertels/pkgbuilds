# https://aur.archlinux.org/packages/gpu-viewer
groups=('modified')

pkgname=gpu-viewer
_app_id=io.github.arunsivaramanneo.GPUViewer
pkgver=2.30
pkgrel=1
pkgdesc="A front-end to glxinfo, vulkaninfo, clinfo and es2_info"
arch=('any')
url="https://github.com/arunsivaramanneo/GPU-Viewer/"
license=('GPL3')
depends=('clinfo' 'gtk4' 'lsb-release' 'mesa-utils' 'python'
         'python-gobject' 'vdpauinfo' 'vulkan-tools')
makedepends=('meson')
checkdepends=('appstream-glib')
optdepends=('nvidia: Vulkan NVIDIA drivers'
            'vulkan-radeon: Vulkan AMD drivers'
            'vulkan-intel: Vulkan Intel drivers')
source=("$pkgname-$pkgver.tar.gz::$url/archive/V$pkgver.tar.gz")
sha256sums=('caf8137245b842ca3eccb8c1f9c30cc76b628e5f0411dfed1aab2fc599e61d75')

build() {
  arch-meson GPU-Viewer-$pkgver build
  meson compile -C build
}

check() {
  cd GPU-Viewer-$pkgver
  appstream-util validate-relax --nonet "data/$_app_id.metainfo.xml"
  desktop-file-validate "data/$_app_id.desktop"
}

package() {
  meson install -C build --destdir "$pkgdir"
}
