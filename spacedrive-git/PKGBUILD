# https://aur.archlinux.org/packages/spacedrive-git
groups=('modified')

## WIP

pkgname=spacedrive-git
pkgver=0.2.0.r1.g74ff99edb
pkgrel=1
pkgdesc="An open source cross-platform file explorer, powered by a virtual distributed filesystem written in Rust."
arch=('x86_64')
url="https://www.spacedrive.com"
license=('GPL-3.0-or-later')
depends=('ffmpeg' 'gtk3' 'libheif' 'webkit2gtk')
makedepends=('cargo' 'clang' 'git' 'pnpm')
optdepends=('gst-plugins-bad' 'gst-plugins-ugly')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=('!lto')
source=('git+https://github.com/spacedriveapp/spacedrive.git'
        "${pkgname%-git}.desktop")
sha256sums=('SKIP'
            '513a57bacd8e4c27a3e6dd0aef2d822bc60bbef1ff1d58879db6f00dc3ae89ae')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname%-git}"
  export PNPM_HOME="$srcdir/pnpm-home"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  pnpm i --frozen-lockfile
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${pkgname%-git}"
  export PNPM_HOME="$srcdir/pnpm-home"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  pnpm prep
  pnpm tauri build --bundles app
}

package() {
  cd "${pkgname%-git}"
  install -Dm755 "apps/desktop/src-tauri/target/release/${pkgname%-git}" -t \
    "$pkgdir/usr/bin/"
  install -Dm755 apps/desktop/src-tauri/target/release/*.so -t \
    "$pkgdir/usr/lib/${pkgname%-git}/"

  install -Dm644 apps/.deps/models/yolov8s.onnx -t \
    "$pkgdir/usr/share/${pkgname%-git}/"

  for i in 32x32 128x128 128x128@2x; do
    install -Dm644 "apps/desktop/src-tauri/icons/${i}.png" \
      "$pkgdir/usr/share/icons/hicolor/${i}/apps/${pkgname%-git}.png"
  done
  install -Dm644 apps/desktop/src-tauri/icons/icon.png \
    "$pkgdir/usr/share/icons/hicolor/512x512/apps/${pkgname%-git}.png"

  install -Dm644 "$srcdir/${pkgname%-git}.desktop" -t \
    "$pkgdir/usr/share/applications/"
}
