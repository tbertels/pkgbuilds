# https://www.archlinux.org/packages/community/x86_64/zsh-theme-powerlevel10k
groups=('modified')

pkgname=zsh-theme-powerlevel10k
# Whenever pkgver is updated, _libgit2ver below must also be updated.
pkgver=1.15.0
_libgit2ver="tag-82cefe2b42300224ad3c148f8b1a569757cc617a"
pkgrel=2
pkgdesc="Powerlevel10k is a theme for Zsh. It emphasizes speed, flexibility and out-of-the-box experience."
arch=('x86_64')
url='https://github.com/romkatv/powerlevel10k'
license=('MIT')
makedepends=('git' 'cmake')
depends=('glibc' 'zsh')
optdepends=(
  'ttf-meslo-nerd-font-powerlevel10k: recommended font'
  'powerline-fonts: patched fonts for powerline')
  # It works well with Nerd Fonts, Source Code Pro, Font Awesome, Powerline,
  # and even the default system fonts. The full choice of style options is
  # available only when using Nerd Fonts.
replaces=('zsh-theme-powerlevel9k')

# _libgit2ver depends on pkgver. They must be updated together. See libgit2_version in:
# https://raw.githubusercontent.com/romkatv/powerlevel10k/v${pkgver}/gitstatus/build.info
source=(
  "powerlevel10k-${pkgver}.tar.gz::https://github.com/romkatv/powerlevel10k/archive/v${pkgver}.tar.gz"
  "https://github.com/romkatv/powerlevel10k/releases/download/v$pkgver/powerlevel10k-$pkgver.tar.gz.asc"
  "libgit2-${_libgit2ver}.tar.gz::https://github.com/romkatv/libgit2/archive/${_libgit2ver}.tar.gz")
sha256sums=('3482ae9b876890b78dd2567066cf5ceecd37eb2117c12248106710655a7dc1dc'
            'SKIP'
            'dc701c4e2080f7901f5d599d642b629569e5581086b9838d481d09f284dc7621')
validpgpkeys=('8B060F8B9EB395614A669F2A90ACE942EB90C3DD') # Roman Perepelitsa <roman.perepelitsa@gmail.com>

build() {
  cd "${srcdir}/libgit2-${_libgit2ver}"
  cmake \
   -DCMAKE_BUILD_TYPE=None \
   -DZERO_NSEC=ON \
   -DTHREADSAFE=ON \
   -DUSE_BUNDLED_ZLIB=ON \
   -DREGEX_BACKEND=builtin \
   -DUSE_HTTP_PARSER=builtin \
   -DUSE_SSH=OFF \
   -DUSE_HTTPS=OFF \
   -DBUILD_CLAR=OFF \
   -DUSE_GSSAPI=OFF \
   -DUSE_NTLMCLIENT=OFF \
   -DBUILD_SHARED_LIBS=OFF \
   -DENABLE_REPRODUCIBLE_BUILDS=ON \
   -Wno-dev \
   .
  make

  # build gitstatus
  cd "$srcdir/powerlevel10k-${pkgver}/gitstatus"
  export CXXFLAGS+=" -I${srcdir}/libgit2-${_libgit2ver}/include -DGITSTATUS_ZERO_NSEC -D_GNU_SOURCE"
  export LDFLAGS+=" -L${srcdir}/libgit2-${_libgit2ver}"
  make
}

package() {
  cd "$srcdir/powerlevel10k-${pkgver}"
  find . -type f -exec install -D '{}' "$pkgdir/usr/share/${pkgname}/{}" ';'

  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s "/usr/share/${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}"

  # delete unnecessary files. See also: https://bugs.archlinux.org/task/66737
  rm -rf "${pkgdir}/usr/share/${pkgname}/gitstatus/obj"
  rm -rf "${pkgdir}/usr/share/${pkgname}/gitstatus/.gitignore"
  rm -rf "${pkgdir}/usr/share/${pkgname}/gitstatus/.gitattributes"
  rm -rf "${pkgdir}/usr/share/${pkgname}/gitstatus/src"
  rm -rf "${pkgdir}/usr/share/${pkgname}/gitstatus/build"
  rm -rf "${pkgdir}/usr/share/${pkgname}/gitstatus/deps"
  rm -rf "${pkgdir}/usr/share/${pkgname}/gitstatus/Makefile"
  rm -rf "${pkgdir}/usr/share/${pkgname}/gitstatus/mbuild"
  rm "${pkgdir}/usr/share/${pkgname}/.gitattributes"
  rm "${pkgdir}/usr/share/${pkgname}/.gitignore"
  rm -rf "${pkgdir}/usr/share/${pkgname}/gitstatus/usrbin/.gitkeep"
  rm "${pkgdir}/usr/share/${pkgname}/gitstatus/.clang-format"
  rm -rf "${pkgdir}/usr/share/${pkgname}/gitstatus/.vscode/"

  cd "${pkgdir}/usr/share/${pkgname}"
  for file in *.zsh-theme internal/*.zsh gitstatus/*.zsh gitstatus/install; do
    zsh -fc "emulate zsh -o no_aliases && zcompile -R -- $file.zwc $file"
  done
}
