# https://aur.archlinux.org/packages/paru
groups=('modified')

pkgname=paru
pkgver=2.0.1+15+g81efb1f
pkgrel=1
pkgdesc='Feature packed AUR helper'
arch=('i686' 'pentium4' 'x86_64' 'arm' 'armv7h' 'armv6h' 'aarch64')
url="https://github.com/morganamilo/paru"
license=('GPL3')
depends=('git' 'pacman')
makedepends=('cargo')
optdepends=('bat: colored pkgbuild printing'
            'devtools: build in chroot and downloading pkgbuilds')
backup=("etc/$pkgname.conf")
_commit=81efb1f810f7265f6bf7cf8637a5103f6836f56a  # branch/master
source=("git+https://github.com/morganamilo/paru.git#commit=${_commit}?signed")
sha256sums=('SKIP')
validpgpkeys=('F850562FCDA369F80D33000AE48D0A8326DE47C5') # Morgan Adamiec <morganamilo@archlinux.org>

pkgver() {
  cd "$pkgname"
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd "$pkgname"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build () {
  cd "$pkgname"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  if pacman -T pacman-git > /dev/null; then
    _features+="git,"
  fi

  if [[ $CARCH != x86_64 ]]; then
    export CARGO_PROFILE_RELEASE_LTO=off
  fi

  cargo build --frozen  --release --features "${_features:-}"
  ./scripts/mkmo locale/
}

package() {
  cd "$pkgname"
  install -Dm755 "target/release/$pkgname" -t "${pkgdir}/usr/bin/"
  install -Dm644 "$pkgname.conf" -t "${pkgdir}/etc/"
  install -Dm644 "man/$pkgname.8" -t "$pkgdir/usr/share/man/man8/"
  install -Dm644 "man/$pkgname.conf.5" -t "$pkgdir/usr/share/man/man5/"
  install -Dm644 completions/bash \
    "${pkgdir}/usr/share/bash-completion/completions/$pkgname.bash"
  install -Dm644 completions/fish \
    "${pkgdir}/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -Dm644 completions/zsh \
    "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  cp -r locale "${pkgdir}/usr/share"
}
