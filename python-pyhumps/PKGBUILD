pkgname=python-pyhumps
_name=${pkgname#python-}
pkgver=3.0.2
pkgrel=1
pkgdesc="Convert strings (and dictionary keys) between snake case, camel case and pascal case in Python. Inspired by Humps for Node"
arch=('any')
url="https://github.com/nficano/humps"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('042b4b6eec6c1f862f8310c0eebbae19293e9edab8cafb030ff78c890ef1aa34')

build() {
  cd "$_name-$pkgver"
  python setup.py build
}

check() {
  cd "$_name-$pkgver"
  pytest
}

package() {
  cd "$_name-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
