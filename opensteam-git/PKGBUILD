# https://aur.archlinux.org/packages/opensteam-git
groups=('modified')

pkgname=opensteam-git
pkgver=0.0.2.beta.r0.ga1ff4de
pkgrel=1
pkgdesc="Partially open-source alternative to the Steam Client application"
arch=('x86_64')
url="https://github.com/20PercentRendered/opensteamclient"
license=('MIT')
depends=(
  'curl'
  'hicolor-icon-theme'
  'lib32-gcc-libs'
  'libarchive'
  'openssl'
  'protobuf'
  'qrencode'
  'qt6-base'
)
makedepends=(
  'cmake'
  'extra-cmake-modules'
  'git'
  'nlohmann-json'
)
checkdepends=(
  'desktop-file-utils'
)
provides=('libmocksteamservice.so')
source=('git+https://github.com/20PercentRendered/opensteamclient.git'
        'git+https://github.com/SteamDatabase/Protobufs.git')
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd "$srcdir/opensteamclient"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/opensteamclient"
  git submodule init
  git config submodule.ext/SteamDatabase/Protobufs.url "$srcdir/Protobufs"
  git -c protocol.file.allow=always submodule update
}

build() {
  cmake -B build -S opensteamclient \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DREL_BUILD=1 \
    -Wno-dev
  cmake --build build
}

check() {
  cd "$srcdir/opensteamclient"
  desktop-file-validate package/opensteamclient.desktop
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$srcdir/opensteamclient"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
