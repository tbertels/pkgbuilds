# This is the last release supporting Pulseaudio. The following PulseEffects
# releases will be based on the new Pipewire multimedia server and only fixes
# to critical bugs will be backported to the legacy pulseaudio branch
# https://github.com/wwmm/pulseeffects/tree/pulseaudio-legacy.

pkgname=pulseeffects-legacy
pkgver=4.8.5
pkgrel=1
pkgdesc='Audio Effects for Pulseaudio Applications'
arch=('x86_64')
url='https://github.com/wwmm/pulseeffects'
license=('GPL3')
depends=('gtk3' 'gtkmm3' 'glibmm' 'pulseaudio' 'gstreamer' 'gst-plugin-gtk'
         'gst-plugins-bad' 'lilv' 'boost-libs' 'libsigc++' 'libsndfile'
         'libsamplerate' 'zita-convolver' 'libebur128')
makedepends=('meson' 'boost' 'itstool' 'appstream-glib' 'zam-plugins'
             'rubberband' 'rnnoise')
optdepends=('calf: limiter, compressor exciter, bass enhancer and others'
            'lsp-plugins: equalizer, delay'
            'rubberband: pitch shifting'
            'zam-plugins: maximizer'
            'rnnoise: noise remover'
            'yelp: in-app help')
provides=('pulseeffects')
conflicts=('pulseeffects')
source=("pulseeffects-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('df1c4c4a9811c62a549822dacde3a9e36233ba3ec58817ae52a236f6181a507c')

build() {
	arch-meson pulseeffects-$pkgver build
	meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
