# https://aur.archlinux.org/packages/gnome-shell-extension-rounded-window-corners-git
groups=('modified')

pkgname=gnome-shell-extension-rounded-window-corners-git
pkgver=4.r10.g508abee
pkgrel=1
pkgdesc="A GNOME Shell extension that adds rounded corners for all windows"
arch=('any')
url="https://github.com/yilozt/rounded-window-corners"
license=('GPL3')
depends=('gnome-shell')
makedepends=('git' 'gobject-introspection' 'yarn')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/yilozt/rounded-window-corners.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/rounded-window-corners"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/rounded-window-corners"
  yarn config set cache-folder "$srcdir/yarn-cache"
  yarn install
  yarn build

  pushd _build
  gnome-extensions pack \
            --extra-source=dbus \
            --extra-source=effect \
            --extra-source=manager \
            --extra-source=preferences \
            --extra-source=utils \
            --extra-source=app.js \
            --extra-source=stylesheet.css \
            --extra-source=stylesheet-prefs.css \
            --force
  popd
}

package() {
  cd "$srcdir/rounded-window-corners"
  _uuid='rounded-window-corners@yilozt'

  install -d "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}"
  bsdtar -xvf "_build/${_uuid}.shell-extension.zip" -C "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/"

  install -Dm644 _build/schemas/org.gnome.shell.extensions.rounded-window-corners.gschema.xml -t \
    "$pkgdir/usr/share/glib-2.0/schemas/"
  rm -rf "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/schemas/"
}
