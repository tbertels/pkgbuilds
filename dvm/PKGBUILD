# https://aur.archlinux.org/packages/dvm
groups=('modified')

pkgname=dvm
pkgver=1.1.9
pkgrel=1
pkgdesc="Discord version manager"
arch=('x86_64')
url="https://github.com/diced/dvm"
license=('MIT')
depends=('gcc-libs' 'openssl-1.1')
makedepends=('cargo-nightly')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('a66406b1b244da9aecd6eaff06df357aecfbd6b650755a36190cf40ab593b333')

prepare() {
  cd "$pkgname-$pkgver"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=nightly
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=nightly
  export CARGO_TARGET_DIR=target
  export OPENSSL_DIR=/usr/lib/openssl-1.1
  export X86_64_UNKNOWN_LINUX_GNU_OPENSSL_LIB_DIR=/usr/lib/openssl-1.1
  export X86_64_UNKNOWN_LINUX_GNU_OPENSSL_INCLUDE_DIR=/usr/include/openssl-1.1
  cargo build --frozen --release --all-features
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "target/release/$pkgname" -t "$pkgdir/usr/bin/"
}
