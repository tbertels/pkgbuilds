# https://aur.archlinux.org/packages/appeditor-git
groups=('modified')

pkgname=appeditor-git
pkgver=1.1.1.r4.gbb1b1fb
pkgrel=1
pkgdesc="Allows you to edit application entries in the application menu."
arch=('x86_64')
url="https://github.com/donadigo/appeditor"
license=('GPL3')
depends=('gtk3' 'granite')
makedepends=('git' 'meson' 'vala')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/donadigo/appeditor.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	arch-meson "${pkgname%-git}" build
	meson compile -C build
}

# No tests defined
#check() {
#	meson test -C build --print-errorlogs
#}

package() {
	DESTDIR="$pkgdir" meson install -C build

	ln -s /usr/bin/com.github.donadigo.appeditor "$pkgdir/usr/bin/${pkgname%-git}"
}
