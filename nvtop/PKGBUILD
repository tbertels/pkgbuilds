# https://archlinux.org/packages/community/x86_64/nvtop/

pkgname=('nvtop-nvidia' 'nvtop-amd')
pkgbase=nvtop
pkgver=2.0.2
pkgrel=0
pkgdesc="Neat Videocard TOP, an (h)top like task monitor for AMD and NVIDIA GPUs."
arch=('x86_64')
url="https://github.com/Syllo/nvtop"
license=('GPL3')
depends=('libdrm' 'nvidia-utils' 'ncurses')
makedepends=('cmake' 'cuda')
source=("https://github.com/Syllo/nvtop/archive/$pkgver/$pkgbase-$pkgver.tar.gz")
sha512sums=('8cc4859ae26428b8b4835d5205be4533c38ab90f5a369261b2650200bab16f7953c4d012dba375be5d1c793ca5031775c64b18f1629cae381d6f1e3b29992cb5')

build() {
  cmake -B build-nvidia -S "$pkgbase-$pkgver" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DNVIDIA_SUPPORT=ON \
    -DAMDGPU_SUPPORT=OFF \
    -Wno-dev
  cmake --build build-nvidia

  cmake -B build-amd -S "$pkgbase-$pkgver" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DNVIDIA_SUPPORT=OFF \
    -DAMDGPU_SUPPORT=ON \
    -Wno-dev
  cmake --build build-amd
}

package_nvtop-nvidia() {
  pkgdesc="Neat Videocard TOP, an (h)top like task monitor for NVIDIA GPUs."
  depends=('nvidia-utils' 'ncurses')
  provides=("$pkgbase")
  conflicts=("$pkgbase")

  DESTDIR="$pkgdir" cmake --install build-nvidia
}

package_nvtop-amd() {
  pkgdesc="Neat Videocard TOP, an (h)top like task monitor for AMD GPUs."
  depends=('libdrm' 'ncurses')
  provides=("$pkgbase")
  conflicts=("$pkgbase")

  DESTDIR="$pkgdir" cmake --install build-amd
}
