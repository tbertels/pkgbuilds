# https://aur.archlinux.org/packages/opencl-caps-viewer
groups=('modified')

pkgname=opencl-caps-viewer
pkgver=1.20
pkgrel=1
pkgdesc="OpenCL Hardware Capability Viewer"
arch=('x86_64')
url="https://opencl.gpuinfo.org/"
license=('GPL2')
depends=('opencl-icd-loader' 'qt5-x11extras')
makedepends=('git')
_commit=fd813b28108521d6d6f530d9b65fb38a89926984  # tags/1.20^
source=("git+https://github.com/SaschaWillems/OpenCLCapsViewer.git#commit=${_commit}"
        'git+https://github.com/KhronosGroup/OpenCL-Headers.git')
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd "$srcdir/OpenCLCapsViewer"
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd "$srcdir/OpenCLCapsViewer"
  git submodule init
  git config submodule.OpenCL-Headers.url "$srcdir/OpenCL-Headers"
  git -c protocol.file.allow=always submodule update

  # Correct binary install path
  sed -i 's|target.path = /opt/$${TARGET}/bin|target.path = /usr/bin|g' OpenCLCapsViewer.pro
}

build() {
  cd "$srcdir/OpenCLCapsViewer"
  qmake-qt5 OpenCLCapsViewer.pro \
    DEFINES+=X11 \
    CONFIG+=release \
    PREFIX=/usr
  make
}

package() {
  cd "$srcdir/OpenCLCapsViewer"
  make INSTALL_ROOT="$pkgdir/" install

  install -m644 Resources/icon.png "${pkgdir}"/usr/share/icons/hicolor/256x256/apps/openclCapsViewer.png
}
