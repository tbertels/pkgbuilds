# https://aur.archlinux.org/packages/mozillavpn
groups=('modified')

pkgname=mozillavpn
pkgver=2.4.1
pkgrel=1
pkgdesc="A fast, secure and easy to use VPN. Built by the makers of Firefox."
arch=('x86_64')
url="https://vpn.mozilla.org"
license=('GPL')
depends=('hicolor-icon-theme' 'polkit' 'qt5-charts' 'qt5-declarative' 'qt5-graphicaleffects'
         'qt5-imageformats' 'qt5-networkauth' 'qt5-quickcontrols' 'qt5-quickcontrols2'
         'qt5-svg' 'resolvconf' 'wireguard-tools')
makedepends=('go' 'git' 'python-glean_parser' 'python-pyhumps' 'qt5-tools')
conflicts=('mozlla-vpn-client')
replaces=('mozilla-vpn-client')
source=("git+https://github.com/mozilla-mobile/mozilla-vpn-client.git#tag=v$pkgver"
        'git+https://github.com/KDAB/android_openssl.git'
        'git+https://github.com/WireGuard/wireguard-apple.git'
        'git+https://github.com/WireGuard/wireguard-go.git'
        'git+https://github.com/WireGuard/wireguard-tools.git'
        'git+https://github.com/mozilla-l10n/mozilla-vpn-client-l10n.git')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd "$srcdir/mozilla-vpn-client"

  git submodule init 3rdparty/openSSL
  git config submodule.android_openssl.url "$srcdir/android_openssl"

  git submodule init 3rdparty/wireguard-apple
  git config submodule.wireguard-apple.url "$srcidr/wireguard-apple"

  git submodule init 3rdparty/wireguard-go
  git config submodule.wireguard-go.url "$srcdir/wireguard-go"

  git submodule init 3rdparty/wireguard-tools
  git config submodule.wireguard-tools.url "$srcdir/wireguard-tools"

  git submodule init i18n
  git config submodule.mozilla-vpn-client-l10n.url "$srcdir/mozilla-vpn-client-l10n"

  git submodule update

  # Prevent creation of a `go` directory in one's home.
  # Sometimes this directory cannot be removed with even `rm -rf` unless
  # one becomes root or changes the write permissions.
  export GOPATH="$srcdir/gopath"
  go clean -modcache
}

build() {
  cd "$srcdir/mozilla-vpn-client"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  # glean
  ./scripts/generate_glean.py

  # translations
  ./scripts/importLanguages.py

  qmake \
    QMAKE_CFLAGS="$CFLAGS" \
    QMAKE_CXXFLAGS="$CXXFLAGS" \
    QMAKE_LFLAGS="$LDFLAGS" \
    CONFIG+=production
  make

  # Clean mod cache for makepkg -C
  go clean -modcache
}

package() {
  cd "$srcdir/mozilla-vpn-client"
  make INSTALL_ROOT="$pkgdir" install
}
