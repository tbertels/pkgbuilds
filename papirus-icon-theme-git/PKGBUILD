# https://aur.archlinux.org/packages/papirus-icon-theme-git
groups=('modified')

pkgname=papirus-icon-theme-git
pkgver=20210302.r71.g1764751176
pkgrel=1
epoch=1
pkgdesc="A free and open source SVG icon theme based on Paper Icon Set"
arch=('any')
url="https://git.io/papirus-icon-theme"
license=('LGPL3')
depends=('gtk-update-icon-cache')
makedepends=('git')
optdepends=('hardcode-fixer-git: To deal with hardcoded application icons'
            'hardcode-tray-git: To fix hardcoded tray icons'
#            'sif-git: To fix icons of running Steam games'
            'gnome-shell-extension-appindicator: Integrates AppIndicators on GNOME')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=('!strip')
source=('git+https://github.com/PapirusDevelopmentTeam/papirus-icon-theme.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
	cd "$srcdir/${pkgname%-git}"
	make DESTDIR="$pkgdir/" install
}
