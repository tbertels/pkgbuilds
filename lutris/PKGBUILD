# https://www.archlinux.org/packages/community/any/lutris
groups=('modified')

pkgname=lutris
pkgver=0.5.10.1
pkgrel=1
pkgdesc='Open Gaming Platform'
arch=('any')
url='https://lutris.net'
license=('GPL3')
depends=('dbus-python' 'cabextract' 'curl' 'glib2' 'gtk3' 'libnotify' 'mesa-utils'
         'p7zip' 'psmisc' 'python-distro' 'python-gobject' 'python-lxml'
         'python-magic' 'python-pillow' 'python-requests' 'python-yaml' 'unzip'
         'webkit2gtk' 'xorg-xrandr')
makedepends=('meson')
checkdepends=('appstream-glib')
optdepends=(
  'gamemode: Allows games to request a temporary set of optimisations'
  'gamescope: Draw the game window isolated from your desktop'
  'gnome-desktop: Get display and resolution using GnomeDesktop'
  'gvfs: GVFS backend'
  'innoextract: Extract Inno Setup installers'
  'lib32-gamemode: Allows games to request a temporary set of optimisations'
  'lib32-glibc: for 32bit games support'
  "lib32-mangohud: Display the games' FPS + other information"
  'lib32-vkd3d: Vulkan 3D support'
  'lib32-vulkan-icd-loader: Vulkan support'
  'linux-steam-integration: start Steam with LSI'
  "mangohud: Display the games' FPS + other information"
  'python-evdev: Controller support'
  'vkd3d: Vulkan 3D support'
  'vulkan-icd-loader: Vulkan support'
  'wine: easiest way to get all the libraries missing from the Lutris runtime'
  'winetricks: use system winetricks'
  'xorg-xgamma: Restore gamma on game exit')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lutris/lutris/archive/v$pkgver.tar.gz")
sha512sums=('ebb5709f0287c4726702bc280799dee26849424f9eb226891e97c2644da1acd5a1fd7b0adf0ef67bb71c3c993857242807bb145d10eaa0a5722ec5b346c62ae0')

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
