# https://www.archlinux.org/packages/community/any/lutris
# https://gitlab.manjaro.org/packages/community/lutris
groups=('modified')

pkgname=lutris
_pkgver=0.5.11-beta1
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc='Open Gaming Platform'
arch=('any')
url='https://lutris.net'
license=('GPL3')
depends=('cabextract' 'curl' 'glib2' 'gtk3' 'mesa-utils' 'p7zip' 'psmisc'
         'python-dbus' 'python-distro' 'python-gobject' 'python-lxml'
         'python-pillow' 'python-requests' 'python-yaml' 'unzip'
         'webkit2gtk' 'xdg-utils' 'xorg-xrandr')
makedepends=('meson')
checkdepends=('appstream-glib')
optdepends=(
  'gamemode: Allows games to request a temporary set of optimisations'
  'gamescope: Draw the game window isolated from your desktop'
  'gnome-desktop: Get display and resolution using GnomeDesktop'
  'gvfs: GVFS backend'
  'innoextract: Extract Inno Setup installers'
  'lib32-gamemode: Allows games to request a temporary set of optimisations'
  'lib32-glibc: for 32bit games support'
  "lib32-mangohud: Display the games' FPS + other information"
  'lib32-vkd3d: Vulkan 3D support'
  'lib32-vulkan-icd-loader: Vulkan support'
  'linux-steam-integration: start Steam with LSI'
  "mangohud: Display the games' FPS + other information"
  'python-evdev: Controller support'
  'vkd3d: Vulkan 3D support'
  'vulkan-icd-loader: Vulkan support'
  'wine: easiest way to get all the libraries missing from the Lutris runtime'
  'winetricks: use system winetricks'
  'xorg-xgamma: Restore gamma on game exit')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lutris/lutris/archive/v$_pkgver.tar.gz")
sha512sums=('764e8a2c2c59d094708bff3c81acdaba09aa0d9d37c304acf7ddf0fa900db0e064b19dd38933ee62b7e7f6b792518968833a11078c24421db5b2c9b77e093fb6')

build() {
  arch-meson "$pkgname-$_pkgver" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
