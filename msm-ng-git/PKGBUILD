pkgbase=msm-ng-git
pkgname=('libmcp-git'
         'mcp-common-git'
         'mcp-qt-git'
         'mcp-qt-gnome-git'
         'mcp-kcm-git'
#         'mcp-gtk-git'
         )
pkgver=r82.3460a91
pkgrel=1
pkgdesc="Manjaro Control Panel: Next generation of Manjaro Settings Manager"
arch=('x86_64')
url="https://gitlab.com/LordTermor/msm-ng"
license=('GPL3')
depends=('adwaita-qt' 'gtkmm3' 'hwinfo' 'kauth' 'kcmutils' 'kconfigwidgets'
         'kcoreaddons' 'ki18n' 'kirigami2' 'libpamac' 'libsigc++' 'qt5-declarative')
makedepends=('appstream' 'cmake' 'extra-cmake-modules' 'git' 'nlohmann-json'
             'poco' 'python' 'python-gobject')
options=('debug')
source=('git+https://gitlab.com/LordTermor/msm-ng.git'
        'git+https://github.com/hampusborgos/country-flags.git')
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd "$srcdir/msm-ng"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p kcm

  cd "$srcdir/msm-ng"
  git submodule init
  git config submodule.libmcp/language/country-flags.url "$srcdir/country-flags"
  git submodule update
}

build() {
  cmake -B build -S msm-ng \
    -DCMAKE_BUILD_TYPE='Debug' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_CORE=ON \
    -DBUILD_QT=ON \
    -DBUILD_QT_GNOME=ON \
    -DBUILD_KCMS=ON \
    -DBUILD_GTK=OFF
  cmake --build build
}

package_libmcp-git() {
  pkgdesc="Manjaro Control Panel core library"
  depends=('libsigc++' 'libpamac')
  provides=("${pkgname%-git}" 'libmcp.so')
  conflicts=("${pkgname%-git}" 'msm-ng-core')
  replaces=('msm-ng-core-git')

  DESTDIR="$pkgdir" cmake --install "build/${pkgname%-git}"
}

package_mcp-common-git() {
  pkgdesc="Common files for Manjaro Control Panel"
  depends=('libmcp-git')
  provides=("${pkgname%-git}" 'libmcp-common.so')
  conflicts=("${pkgname%-git}")

  DESTDIR="$pkgdir" cmake --install "build/${pkgname%-git}"

  install -d "$pkgdir/etc/mcp/"
  touch "$pkgdir/etc/mcp/pamac.conf"
}

package_mcp-qt-git() {
  pkgdesc="Manjaro Control Panel Qt GUI"
  depends=('hwinfo' 'kauth' 'kconfigwidgets' 'kcoreaddons'
           'ki18n' 'kirigami2' 'libmcp-git' 'mcp-common-git' 'qt5-declarative')
  provides=("${pkgname%-git}" 'manjaro-settings-manager')
  conflicts=("${pkgname%-git}" 'msm-ng-qt' 'manjaro-settings-manager')
  replaces=('msm-ng-qt-git')

  DESTDIR="$pkgdir" cmake --install "build/${pkgname%-git}"

  mv "$pkgdir"/usr/{lib/qt/plugins,share/kservices5} "$srcdir/kcm/"

  rm "$pkgdir/usr/share/applications/org.manjaro.${pkgname%-git}-gnome.desktop"
}

package_mcp-qt-gnome-git() {
  pkgdesc="Manjaro Control Panel Qt GUI for GNOME"
  depends=('adwaita-qt' 'hwinfo' 'kauth' 'kconfigwidgets' 'kcoreaddons'
           'ki18n' 'kirigami2' 'libmcp-git' 'mcp-common-git' 'qt5-declarative')
  provides=("${pkgname%-git}" 'manjaro-settings-manager')
  conflicts=("${pkgname%-git}" 'msm-ng-qt' 'manjaro-settings-manager')
  replaces=('msm-ng-qt-git')

  DESTDIR="$pkgdir" cmake --install "build/${pkgname%-gnome-git}"

  rm -rf "$pkgdir"/usr/{lib/qt/plugins,share/kservices5}

  rm "$pkgdir/usr/share/applications/org.manjaro.${pkgname%-gnome-git}.desktop"
}

package_mcp-kcm-git() {
  pkgdesc="Manjaro Control Panel KCM modules"
  depends=('hwinfo' 'kcmutils' 'mcp-common-git')
  provides=('msm_mhwd.so' 'mcp_langpack.so' 'mcp_kernel.so')

#  DESTDIR="$pkgdir" cmake --install "build/${pkgname%-git}"

  install -d "$pkgdir"/usr/{lib/qt,share}
  cp -r "$srcdir/kcm/plugins" "$pkgdir/usr/lib/qt/"
  cp -r "$srcdir/kcm/kservices5" "$pkgdir/usr/share/"
}

#package_mcp-gtk-git() {
#  pkgdesc="Manjaro Control Panel Gtk GUI"
#  depends=('gtkmm3' 'libmcp-git' 'mcp-common-git')
#  provides=("${pkgname%-git}" 'manjaro-settings-manager')
#  conflicts=("${pkgname%-git}" 'msm-ng-gtk' 'manjaro-settings-manager')
#  replaces=('msm-ng-gtk-git')

#  DESTDIR="$pkgdir" cmake --install "build/${pkgname%-git}"
#}
