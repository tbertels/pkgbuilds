pkgbase=msm-ng-git
pkgname=('libmcp-git'
         'mcp-common-git'
         'mcp-qt-git'
         'mcp-gtk-git'
         )
pkgver=r47.2ddc47b
pkgrel=1
pkgdesc="Manjaro Control Panel: Next generation of Manjaro Settings Manager"
arch=('x86_64')
url="https://gitlab.com/LordTermor/msm-ng"
license=('GPL3')
depends=('gtkmm3' 'hwinfo' 'kauth' 'kconfigwidgets' 'kcoreaddons' 'ki18n' 'kirigami2' 
         'libpamac' 'libsigc++' 'qt5-declarative')
makedepends=('appstream' 'cmake' 'extra-cmake-modules' 'git' 'nlohmann-json'
             'poco')
source=('git+https://gitlab.com/LordTermor/msm-ng.git'
        'git+https://github.com/hampusborgos/country-flags.git')
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd "$srcdir/msm-ng"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/msm-ng"
  git submodule init
  git config submodule.libmcp/language/country-flags.url "$srcdir/country-flags"
  git submodule update
}

build() {
  cmake -B build -S msm-ng \
    -DCMAKE_INSTALL_PREFIX='/usr'
  cmake --build build
}

package_libmcp-git() {
  pkgdesc="Manjaro Control Panel core library"
  depends=('libsigc++' 'libpamac')
  provides=("${pkgname%-git}" 'libmcp.so')
  conflicts=("${pkgname%-git}" 'msm-ng-core')
  replaces=('msm-ng-core-git')

  DESTDIR="$pkgdir" cmake --install build/libmcp
}

package_mcp-common-git() {
  pkgdesc="Common files for Manjaro Control Panel"
  depends=()
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}")

#  DESTDIR="$pkgdir" cmake --install build/mcp-common
  install -d "$pkgdir/etc/mcp/"
  touch "$pkgdir/etc/mcp/pamac.conf"
}

package_mcp-qt-git() {
  pkgdesc="Manjaro Control Panel Qt GUI"
  depends=('hwinfo' 'kauth' 'kconfigwidgets' 'kcoreaddons' 'ki18n' 'kirigami2'
           'libmcp-git' 'mcp-common-git' 'qt5-declarative')
  provides=("${pkgname%-git}" 'manjaro-settings-manager')
  conflicts=("${pkgname%-git}" 'msm-ng-qt' 'manjaro-settings-manager')
  replaces=('msm-ng-qt-git')

  DESTDIR="$pkgdir" cmake --install build/mcp-qt
}

package_mcp-gtk-git() {
  pkgdesc="Manjaro Control Panel Gtk GUI"
  depends=('gtkmm3' 'libmcp-git' 'mcp-common-git')
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}" 'msm-ng-gtk')
  replaces=('msm-ng-gtk-git')

#  DESTDIR="$pkgdir" cmake --install build/mcp-gtk
  install -Dm755 build/mcp-gtk/mcp-ng-gtk -t "$pkgdir/usr/bin/"
}
