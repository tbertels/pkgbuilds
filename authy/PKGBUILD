# https://aur.archlinux.org/packages/authy
groups=('modified')

pkgname=authy
pkgver=1.9.0
pkgrel=1
pkgdesc="Two factor authentication desktop application"
arch=('x86_64')
url="https://authy.com"
license=('unknown')
depends=('electron9')
makedepends=('squashfs-tools')
conflicts=("$pkgname-snap")
replaces=("$pkgname-snap")
_snapid='H8ZpNgIoPyvmkgxOWw5MSzsXK1wRZiHn'
_snaprev='7'
source=("$pkgname-$pkgver-$_snaprev.snap::https://api.snapcraft.io/api/v1/snaps/download/${_snapid}_${_snaprev}.snap"
        "$pkgname.sh")
sha256sums=('a33f5f40d4bf67ee3800f006aadfa93d396c1150b6d61e416cfad1c1d8215f81'
            'dce87238a960dd0b308e8f19056eea50151093202a0f13f98bebfb3b903b7dac')

prepare() {
  unsquashfs -f -d "$srcdir/$pkgname" "$pkgname-$pkgver-$_snaprev.snap"

  cd "$srcdir/$pkgname"
  sed -i 's|${SNAP}/meta/gui/icon.png|authy|g' "meta/gui/$pkgname.desktop"
}

package() {
  cd "$srcdir/$pkgname"
  install -Dm644 resources/app.asar -t "$pkgdir/usr/lib/$pkgname"
  install -Dm644 "meta/gui/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
  install -Dm644 meta/gui/icon.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
}
