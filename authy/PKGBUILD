# https://aur.archlinux.org/packages/authy
groups=('modified')

pkgname=authy
pkgver=1.8.3
pkgrel=2
pkgdesc="Two factor authentication desktop application"
arch=('x86_64')
url="https://authy.com"
license=('unknown')
depends=('electron9')
makedepends=('squashfs-tools')
conflicts=("$pkgname-snap")
replaces=("$pkgname-snap")
_snapid='H8ZpNgIoPyvmkgxOWw5MSzsXK1wRZiHn'
_snaprev='5'
source=("$pkgname-$pkgver-$_snaprev.snap::https://api.snapcraft.io/api/v1/snaps/download/${_snapid}_${_snaprev}.snap"
        "$pkgname.sh")
sha256sums=('f6be2a19b96a23c626f15f6baa1b51674b84d4f4215495ac6a8155327c9ddbf9'
            '549fdcbb8e833474fd3379a9b3496f3bfe1394487be2078f8c575644593dbe78')

prepare() {
    unsquashfs -f -d "$srcdir/$pkgname" "$pkgname-$pkgver-$_snaprev.snap"

	cd "$srcdir/$pkgname"
    sed -i 's|${SNAP}/meta/gui/icon.png|authy|g' "meta/gui/$pkgname.desktop"
}

package() {
	cd "$srcdir/$pkgname"
    install -d "$pkgdir/usr/lib/$pkgname"
    cp -r resources "$pkgdir/usr/lib/$pkgname"

    install -Dm644 "meta/gui/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
    install -Dm644 meta/gui/icon.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
	install -Dm755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
}
