# https://aur.archlinux.org/packages/catt
groups=('modified')

pkgname=catt
pkgver=0.12.12
pkgrel=1
pkgdesc="Cast All The Things allows you to send videos from many, many online sources to your Chromecast."
arch=('any')
url="https://github.com/skorokithakis/catt"
license=('BSD-2-Clause')
depends=(
  'python-click'
  'python-ifaddr'
  'python-pychromecast'
  'python-requests'
  'yt-dlp'
)
makedepends=(
  'python-installer'
  'python-poetry'
  'python-wheel'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('29b848dedc15b4d698e6b13fab1590d8e0a66844b8ecb320b43334cc133bbfd1')

build() {
  cd "$pkgname-$pkgver"
  GIT_DIR='.' python -m poetry build --format wheel
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644  LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
