# https://archlinux.org/packages/extra/x86_64/libtorrent-rasterbar
pkgname=libtorrent-rasterbar
pkgver=2.0.5+4+g71d608fce
pkgrel=1
pkgdesc="A C++ BitTorrent library that aims to be a good alternative to all the other implementations around"
url="https://www.rasterbar.com/products/libtorrent/"
arch=('x86_64')
license=('BSD')
depends=('boost-libs' 'openssl')
makedepends=('boost' 'cmake' 'git' 'ninja' 'python-setuptools')
options=('!emptydirs')
_commit=71d608fceca7e61c9d124f9ea83f71b06eda3b17
source=("git+https://github.com/arvidn/libtorrent.git#commit=$_commit")
#source=("https://github.com/arvidn/libtorrent/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/libtorrent"
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd "$srcdir/libtorrent"
  git submodule update --init --recursive
}

build() {
  cmake -B build -S libtorrent \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -Dpython-bindings=ON \
    -Dboost-python-module-name=python \
    -Dpython-egg-info=ON \
    -GNinja \
    -Wno-dev
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  cd "$srcdir/libtorrent"
  install -Dm644 COPYING LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
