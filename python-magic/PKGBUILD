# https://archlinux.org/packages/community/any/python-magic
pkgname=python-magic
pkgver=5.40
pkgrel=0
pkgdesc="Python bindings to the magic library"
arch=('any')
url='https://darwinsys.com/file/'
depends=('python')
makedepends=('python-setuptools')
license=('custom')
source=("ftp://ftp.astron.com/pub/file/file-${pkgver}.tar.gz"{,.asc})
sha512sums=('3b70df75fa4c9050d55b1ffdc28e5f3c8b8ef7d4efd1a06bf53f113b676d81114a85aae56e0897d32b53716662d64ad18ab251ca8c92c6405c69eb758bb99afb'
            'SKIP')
validpgpkeys=('BE04995BA8F90ED0C0C176C471112AB16CB33B3A') # "Christos Zoulas (personal key) <christos@zoulas.com>"

build() {
  cd file-${pkgver}/python
  python setup.py build
}

package() {
  cd file-${pkgver}

  pushd python
  export PYTHONHASHSEED=0
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  popd

  install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
