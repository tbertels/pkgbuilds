# https://aur.archlinux.org/packages/journal-viewer
groups=('modified')

pkgname=journal-viewer
pkgver=0.3.0
pkgrel=1
pkgdesc="A modern linux desktop application to visualize systemd logs."
arch=('x86_64')
url="https://github.com/mingue/journal-viewer"
license=('GPL3')
depends=('systemd' 'webkit2gtk')
makedepends=('cargo' 'npm')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz"
        "$pkgname.desktop")
sha256sums=('aa794ab4c852ea9e86b5e8a2dfe7b1c5f99c74c43bbdc606dc92780b3d9cdd32'
            '413001fffca9460ec2a5ff4e5515e12218034747a9bc41b018507a17a2a5ddfc')

prepare() {
  cd "$pkgname-$pkgver"
  npm config set cache "$srcdir/npm-cache"
  npm install

  cd src-tauri
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$CARCH-unknown-linux-gnu"

  # Don't bundle AppImage
  sed -i 's/"targets": "all",/"targets": "deb",/g' tauri.conf.json
}

build() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  npm config set cache "$srcdir/npm-cache"
  npm run build
  npm run tauri build
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "src-tauri/target/release/$pkgname" -t "$pkgdir/usr/bin/"
  install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir/usr/share/applications/"

#  _bundle_dir="src-tauri/target/release/bundle/deb/${pkgname}_0.0.1_amd64"

#  install -Dm644 "${_bundle_dir}/data/usr/share/applications/$pkgname.desktop" -t \
#    "$pkgdir/usr/share/applications/"

#  for icon_size in 32x32 128x128; do
#    install -Dm644 "${_bundle_dir}/data/usr/share/icons/hicolor/${icon_size}/apps/$pkgname.png" -t \
#      "$pkgdir/usr/share/icons/hicolor/${icon_size}/apps/"
#  done
#  install -Dm644 "${_bundle_dir}/data/usr/share/icons/hicolor/256x256@2/apps/$pkgname.png" -t \
#      "$pkgdir/usr/share/icons/hicolor/256x256@2/apps/"
}
