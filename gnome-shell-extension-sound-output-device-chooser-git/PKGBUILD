# https://aur.archlinux.org/packages/gnome-shell-extension-sound-output-device-chooser-git
groups=('modified')

pkgname=gnome-shell-extension-sound-output-device-chooser-git
_gitname=gse-sound-output-device-chooser
pkgver=39.r21.g0702290
pkgrel=1
pkgdesc="Shows a list of sound output and input devices in the status menu"
arch=('any')
url="https://github.com/kgshank/gse-sound-output-device-chooser"
license=('GPL3')
depends=('gnome-shell' 'python')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/kgshank/gse-sound-output-device-chooser.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$_gitname"
  make build
}

package() {
  cd "$srcdir/$_gitname"
  install -d "$pkgdir/usr/share/gnome-shell/extensions"
  make INSTALL_DIR="$pkgdir/usr/share/gnome-shell/extensions" install

  _uuid='sound-output-device-chooser@kgshank.net'

  install -Dm644 "$_uuid/schemas/org.gnome.shell.extensions.sound-output-device-chooser.gschema.xml" -t \
    "$pkgdir/usr/share/glib-2.0/schemas"

  rm -rf "$pkgdir/usr/share/gnome-shell/extensions/$_uuid/schemas/"
}
